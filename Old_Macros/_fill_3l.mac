! _fill_3l.mac  Macro creates fillet of unknown radius between 3 lines
!
/NOPR
/PMACRO
IMME,0
*GET,ACSYS,ACTIVE,,CSYS
CM,STARTL,LINE
CM,STARTK,KP
*MSG,INFO
Select the 3 straight lines you would like to fillet
LSEL,R,P
CM,LINES_,LINE
KPLS
CM,KPS_,KP
!	Find middle line
LS=LSNEXT(0)
*DO,LOOP,1,3
LSEL,,,,LS
KPLS
LSKP
CMSEL,R,LINES_
*GET,NUM,LINE,,COUNT
*IF,NUM,EQ,3,THEN
LMID=LS
*ENDIF
CMSEL,,LINES_
LS=LSNEXT(LS)
*ENDDO
CMSEL,,LINES_
KPLS
!	Make local cs
*GET,KP1,LINE,LMID,KP,1
*GET,KP2,LINE,LMID,KP,2
!CMSEL,,KPS_
!KSEL,U,,,KP1
!!KSEL,U,,,KP2
!KP3=KPNEXT(0)
!KPLS
!CSKP,9999,,KP1,KP2,KP3
!	Get LINE KP DEFINITIONS
KSEL,,,,KP1
LSKP
CMSEL,R,LINES_
LSEL,U,,,LMID
LSA=LSNEXT(0)
KPLS
KSEL,U,,,KP1
KPA=KPNEXT(0)
CMSEL,,LINES_
LSEL,U,,,LMID
LSEL,U,,,LSA
LSB=LSNEXT(0)
CMSEL,,KPS_
KSEL,U,,,KP1
KSEL,U,,,KP2
KSEL,U,,,KPA
KPB=KPNEXT(0)
!	Get angles
!ALPHA_A=ABS(ATAN( KX(KPA)-KX(KP1)/KY(KPA)-KY(KP1) ))
!ALPHA_B=ABS(ATAN( (KX(KPB)-KX(KP2)/KY(KPB)-KY(KP2))))
!GAMMA_A=(ALPHA_A+90)/2
!GAMMA_B=(ALPHB_A+90)/2
GAMMA_A=ANGLEK(KP1,KPA,KP2)/2
GAMMA_B=ANGLEK(KP2,KPB,KP1)/2
!	Get distance of lmid
DIST_=DISTKP(KP1,KP2)
RADIUS=DIST_*ABS(TAN(GAMMA_A))/ABS(1+TAN(GAMMA_A)/TAN(GAMMA_B))
*MSG,UI,RADIUS
RADIUS IS %G
!	Let's make some fillets !!
CMSEL,,LINES_
LFILLT,LSA,LMID,RADIUS
LFILLT,LSB,LMID,RADIUS
CMSEL,U,LINES_
LCOMB,ALL
!	RESRT STUFF
CMSEL,A,STARTL
,,STARTK
CSYS,ACSYS
CMDELE,STARTL
,STARTK
,LINES_
,KPS_
PARRES
IMME,1
LPLO
/GOPR

