!                                       MACRO
!
!	FINDS SMALLEST AND LARGEST AREAS FROM THE
!	SELECTED SET OF AREAS
!
!
!	GET NUMBER OF SELECTED AREAS
!
CM,_LSET,AREA
!
!	DO ASUM TO GET THE AREAS CALCULATED
!
ASUM
*GET,_MAXLI,AREA,,COUNT
!
!	GET MINIMUM AREA NUMBER IN SELECTED SUBSET
!
*GET,_MINLI,AREA,,NUM,MIN
*GET,_LEN,AREA,_MINLI,AREA
_LMIN=_LEN
_LMAX=_LEN
_LS=_MINLI
_LL=_MINLI
ASEL,U,AREA,,_MINLI
!
!	LOOP THRU SET TO FIND SMALL AND LARGE AREA
!
_MAXLI=_MAXLI-1
*DO,_I,1,_MAXLI
*GET,_MINLI,AREA,,NUM,MIN
*GET,_LTEST,AREA,_MINLI,AREA
*IF,_LTEST,GT,_LMAX,THEN
_LMAX=_LTEST
_LL=_MINLI
*ENDIF
*IF,_LTEST,LT,_LMIN,THEN
_LMIN=_LTEST
_LS=_MINLI
*ENDIF
ASEL,U,AREA,,_MINLI
*ENDDO
!
!	WRITE OUT THE SMALLEST AND LARGEST VALUES
!	AND CORRESPONDING AREA NUMBERS
!
*MSG,INFO,_LMIN,_LS,_LMAX,_LL
Small Area = %G,   Area Number = %I
*MSG,INFO,_LMAX,_LL
Large Area = %G,   Area Number = %I
CMSEL,S,_LSET
