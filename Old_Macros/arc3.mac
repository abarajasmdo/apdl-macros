/nopr
!
!    arc3.mac   A macro to create an arc through 3 points
!
!        Usage:  arc3,p1,p2,p3
!               where,   p1=first point
!                        p2=second point
!                        p3=third point
!
!         the arc will start at p1, go through p2 and end at p3
! 
!  get coordinates for p1
*get,x1,kx,arg1
*get,y1,ky,arg1
*get,z1,kz,arg1
! 
!  get coordinates for p2
*get,x2,kx,arg2
*get,y2,ky,arg2
*get,z2,kz,arg2
! 
!  get coordinates for p3
*get,x3,kx,arg3
*get,y3,ky,arg3
*get,z3,kz,arg3
!
!  calculate some constants
!
c1=((x1-x2)*(x1-x2)+(y1-y2)*(y1-y2)+(z1-z2)*(z1-z2))**.5

c2=((x2-x3)*(x2-x3)+(y2-y3)*(y2-y3)+(z2-z3)*(z2-z3))**.5

c3=((x1-x3)*(x1-x3)+(y1-y3)*(y1-y3)+(z1-z3)*(z1-z3))**.5

beta=acos((c1*c1+c2*c2-c3*c3)/(2*c1*c2))


dum1=(c1-c2*(cos(beta)))

dum2=(c2*sin(beta))

alph=atan(dum1/dum2)
!
! calculate the arc radius
r=c2/(2*(cos(alph)))
!
! create the arc
!
larc,arg1,arg3,arg2,-r
!
*msg,info,arg1,arg2,arg3,r
(' Arc Created through points ',3f6.0,/,'Radius =',f6.3)
!
! clear variables from memory
!
c1=
c2=
c3=
x1=
x2=
x3=
y1=
y2=
y3=
z1=
z2=
z3=
r=
beta=
alph=
dum1=
dum2=
/gopr

