!
! ce_avg  macro makes averaging constraint equation between two sets of
!		nodes defined by components
!
!               allows any number of nodes on either side and offset
!
!	By:  Dan Bohlen / Adrian Allen  
PARSAV
CM,USERNODE,NODE
*ASK,ICOMP,FIRST NODE COMP NAME,'COMP_NAME'
*ASK,JCOMP,SECOND NODE COMP NAME,'COMP_NAME'
*ASK,CEDOF,CE DOF (ONLY ONE),'UX'
*ASK,OFFSET,OFFSET VALUE (offset=AVG(%ICOMP%)-avg(%JCOMP%)),0.0
!
!	CHECK THAT THE COMP'S HAVE EQUAL NUMBER OF NODES
!
CMSEL,R,ICOMP
*GET,INNDS,NODE,,COUNT
cmsel,,USERNODE
CMSEL,R,JCOMP
*GET,JNNDS,NODE,,COUNT
!
! GET STARTING CE NUMBER
!
CENUM=CEINQR(,14)+1
!
!	DO POSITIVE COEFF
!
cmsel,,USERNODE
CMSEL,R,ICOMP
N=0
*DO,I,1,INNDS
N=NDNEXT(N)
CE,CENUM,0,N,CEDOF,1/INNDS,
*ENDDO
!
!	NOW DO NEGATIVE COEFF
!
cmsel,,USERNODE
CMSEL,R,JCOMP
N=0
*DO,I,1,JNNDS
N=NDNEXT(N)
CE,CENUM,0,N,CEDOF,-1/JNNDS,
*ENDDO
!
!       SET OFFSET VALUE
!
CECMOD,CENUM,OFFSET
!
!	CLEAN UP
!
CELIST,CENUM
CMSEL,,USERNODE
CMDE,USERNODE
PARRES

