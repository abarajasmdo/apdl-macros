!
! ce_UX macro makes averaging constraint equation between two sets of
!		nodes defined by components
!
!               allows any number of nodes on either side and offset
!
!	DETERMINES THE AVERAGE RADIAL DISPLACMENT IN /POST1
!
!	By:  Dan Bohlen / Adrian Allen  
/NOPR
PARSAV
CM,USERNODE,NODE
*ASK,EREAL,GAP SET REAL CONSTANT NUMBER,
*ASK,ICOMP,FIRST NODE COMP NAME,'COMP_NAME'
*ASK,JCOMP,SECOND NODE COMP NAME,'COMP_NAME'
!*ASK,CEDOF,CE DOF (ONLY ONE),'UX'
CEDOF='UX'
!*ASK,OFFSET,OFFSET VALUE (offset=AVG(%ICOMP%)-avg(%JCOMP%)),0.0
!
!	CHECK THAT THE COMP'S HAVE EQUAL NUMBER OF NODES
!
ESEL,REAL,EREAL
NELEM
CMSEL,R,ICOMP
CM,ITEMP,NODE
*GET,INNDS,NODE,,COUNT
NELEM
CMSEL,R,JCOMP
CM,JTEMP,NODE
*GET,JNNDS,NODE,,COUNT
!
!	GET AVG DISPLACEMENTS
/POST1
nod=0
totx=0
CMSEL,,ITEMP
*DO,I,1,INNDS
nod=ndnext(nod)
totx=totx+ux(nod)
*enddo
/GOPR
IUX=totx/INNDS
/NOPR
!
nod=0
totx=0
CMSEL,,JTEMP
*DO,I,1,JNNDS
nod=ndnext(nod)
totx=totx+ux(nod)
*enddo
/GOPR
JUX=totx/JNNDS

!
OFFSET=IUX-JUX
/NOPR
!
! GET STARTING CE NUMBER
!
/PREP7
/GOPR
CENUM=CEINQR(,14)+1
/NOPR
!
!	DO POSITIVE COEFF
!
CMSEL,,ITEMP
N=0
*DO,I,1,INNDS
N=NDNEXT(N)
CE,CENUM,0,N,CEDOF,1/INNDS,
*ENDDO
!
!	NOW DO NEGATIVE COEFF
!
CMSEL,,JTEMP
N=0
*DO,I,1,JNNDS
N=NDNEXT(N)
CE,CENUM,0,N,CEDOF,-1/JNNDS,
*ENDDO
!
!       SET OFFSET VALUE
!
CECMOD,CENUM,OFFSET
!
!	CLEAN UP
!
!CELIST,CENUM
CMSEL,,USERNODE
CMDE,USERNODE
CMDEL,ITEMP
,JTEMP
PARRES

