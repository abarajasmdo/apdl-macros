/nopr
! Macro to plot and label components
! if the ARG1 is 0, then goes thru all
! otherwise just plots,ARG1(start),ARG2(last),ARG3 (incr)
! ARG4 is flag to restore the previous selections of elems/nodes
! default ARG4=0  dont do it.
! Osman Buyukisik     3-0404
/sys,rm -f __an3d.txt
cm,_foo1,node
cm__foo2,elem
! get the number of components
*get,num_c,COMP,,NCOMP
*if,num_c,le,0,:err1
c_0=arg1
c_1=arg2
c_2=arg3
*if,c_0,le,0,then
  c_0=1
  c_1=num_c
  c_2=1
*else
  *if,c_1,le,0,then
    c_1=c_0
    c_2=0
  *else
    *if,c_2,le,0,then
      c_2=1
    *endif
  *endif
*endif
! do an nplot to set the view
nplot
/user,1
! we need the next due to ansys 5.5 "feature"
! somehow the %xxx% works twice, results in incorrect labels
/sys,rm -f __an3d.txt
*cfopen,__an3d,txt
*do,_i,1,num_c
  *get,name_c,COMP,_i,NAME
  *cfwr,%name_c% =
*enddo
*cfclose
/inp,__an3d,txt
_j=0
*cfopen,__an3d,txt
*do,_i,c_0,c_1,c_2
  *get,name_c,COMP,_i,NAME
  *get,type_c,COMP,%name_c%,TYPE
  *if,type_c,eq,1,then    ! nodes
     _j=_j+1
     *if,_j,eq,1,then
       /era
     *else
       /noer
     *endif
     cmsel,,%name_c%
     nplot
     *get,num_no,node,,count
     __x=0
     __y=0
     __z=0
     __nod=0
     *do,_k,1,num_no
      __nod=ndnext(__nod)
       __x=__x+nx(__nod)  
       __y=__y+ny(__nod)  
       __z=__z+nz(__nod)  
     *enddo
     __x=__x/num_no
     __y=__y/num_no
     __z=__z/num_no
     *cfwr,/an3d,text,__x,__y,__z,%name_c%
  *elseif,type_c,eq,2,then   ! elements
     _j=_j+1
     *if,_j,eq,1,then
       /era
     *else
       /noer
     *endif
     cmsel,,%name_c%
     nelem
     eplot
     *get,num_no,node,,count
     __x=0
     __y=0
     __z=0
     __nod=0
     *do,_k,1,num_no
      __nod=ndnext(__nod)
       __x=__x+nx(__nod)  
       __y=__y+ny(__nod)  
       __z=__z+nz(__nod)  
     *enddo
     __x=__x/num_no
     __y=__y/num_no
     __z=__z/num_no
     *cfwr,/an3d,text,__x,__y,__z,%name_c%
  *endif
*enddo
*cfclose
/inp,__an3d,txt
/noer
/replo
*do,_i,c_0,c_1,c_2
  *get,name_c,COMP,_i,NAME
  *get,type_c,COMP,%name_c%,TYPE
  *if,_i,eq,c_0,then
     cmsel,,%name_c%
     /era
     *if,type_c,eq,1,then
       nplot
     *else
!       nelem
       eplo
     *endif
  *else
     cmsel,a,%name_c%
     /noer
     *if,type_c,eq,1,then
       nplot
     *else
!      nelem
       eplo
     *endif 
  *endif
*enddo
*if,arg4,gt,0,then
  cmsel,,_foo1
  cmsel,,_foo2
*endif
cmdel,_foo1
cmdel,_foo2
/nopr
:err1
/era
__x=
__y=
__z=
_i=
_j=
_k=
num_no=
name_c=
num_c=
type_c=
c_0=
c_1=
c_2=
/gopr
