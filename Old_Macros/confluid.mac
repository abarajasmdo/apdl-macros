!  THIS MACRO CONNECTS EXISTING, SELECTED, LINEAR SURF22
!  ELEMENTS TO THE NODES OF EXISTING, SELECTED FLUID66
!  ELEMENTS BASED ON GEOMETRIC PROXIMITY, I.E., CONNECT
!  TO THE NODE CLOSEST TO THE CENTROID OF THE ELEMENT
!
!     WRITTEN BY DR. LAWRENCE DUROCHER, CAEA
!                    9/29/94
!  MAKE A COMPONENT OF ALL SELECTED ELEMENTS
CM,SELECTEL,ELEM
!
!  MAKE A COMPONENT OF ALL SELECTED NODES
!
CM,SELECTND,NODE
!
!  RESELECT THE FLUID66 ELEMENTS AND THE NODES ASSOCIATED
!  WITH THESE ELEMENTS
!
ESEL,R,ENAME,,66
NSLE,S
!
!  SELECT ALL THE SURF22 ELEMENTS THAT WERE PART OF THE COMPONENT
!
ESEL,NONE
CMSEL,S,SELECTEL
ESEL,R,ENAME,,22
!
!  NOTE ONLY THE FLUID66 NODES ARE SELECTED
!
!  GET THE NUMBER OF SURF22 ELEMENTS THAT ARE SELECTED
!
*GET,NUMSF22,ELEM,,COUNT
!
!  LOOP THRU TO CONNECT UP THE FLOW AND CONVECTION ELEMENTS
!
*DO,I,1,NUMSF22
!
!  GET MIN. ELEEMENT NUMBER OF THE SELECTED SURF22 ELEMENTS
!
*GET,EMIN,ELEM,,NUM,MIN
!
!  GET THE EXISTING 4 NODE NUMBERS ON THE SURFACE
!
N1=NELEM(EMIN,1)
N2=NELEM(EMIN,2)
N3=NELEM(EMIN,3)
N4=NELEM(EMIN,4)
!
!  GET THE AVERAGE LOCATION OF ELEMENT "EMIN"
!
XAV=(NX(N1)+NX(N2)+NX(N3)+NX(N4))/4.
YAV=(NY(N1)+NY(N2)+NY(N3)+NY(N4))/4.
ZAV=(NZ(N1)+NZ(N2)+NZ(N3)+NZ(N4))/4.
!
!  GET THE NODE NUMBER(OF THE SELECTED NODES) OF THE
!  NODE THAT IS CLOSEST TO THIS LOCATION
!
N5=NODE(XAV,YAV,ZAV)
!
!  MODIFY THE SURF22 TO INCLUDE THE FLUID66 NODE
!
EMODIFY,EMIN,-5,N5
!
!  UNSELECT THIS ELEMENT
!
ESEL,U,ELEM,,EMIN
*ENDDO
!
!  RESTORE THE ORIGINAL SELECTED NODES AND ELEMENTS
!
CMSEL,S,SELECTEL
CMSEL,S,SELECTND
