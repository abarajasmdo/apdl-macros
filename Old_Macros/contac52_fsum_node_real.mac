/COM
/COM  contac12_fsum_node_realSUM_NODE_REAL       ANSYS 5.5   VER 1.0   06/02/00     R.P. ZIEGLER
/COM
/COM
/COM    THE CONTAC52 ELEMENTS ARE SELECTED BY REAL CONSTANT.
/COM
/COM    THE NORMAL AND FRICTION FORCES ARE SUMMED AT THE I & J NODES IN THE RESULTS
/COM
/COM    COORDINATE SYSTEM (RSYS).  EQULIBRIUM CALCULATIONS MAY ALSO BE PERFORMED BY
/COM
/COM    SUMMING THE FRICTION FORCES ON ALL OTHER ATTACHED ELEMENTS AT THE I & J NODES 
/COM
/COM    AND THE DIFFERENCE CALCULATED.  EQULIBRIUM CALCULATIONS WILL BE DONE IN THE
/COM
/COM    IN THE NODAL COORDINATE SYSTEM ( RSYS=-1).
/COM
/COM
/COM    
/COM  ANSYS MODUAL : POST1 
/COM
/COM
/COM  INPUTS : 
/COM
/COM
/COM     ARG1 - OPTION TO CALCULATE I & J NODE ELEMENT FORCES FROM OTHER ELEMENTS
/COM
/COM          = 0   USE CURRENT RESULTS COORDINATE SYSTEMS
/COM                SKIPP OTHER ELEMENT NODAL FORCE EQULIBRIUM CALCULATIONS  ( DEFAULT )
/COM                      
/COM
/COM          = 1   USE NODAL RESULTS COORDINATE SYSTEMS
/COM                PERFORM OTHER ELEMENT NODAL FORCE EQULIBRIUM CALCULATIONS 
/COM
/COM
/COM          = 2   USE NODAL RESULTS COORDINATE SYSTEMS
/COM                PERFORM DETAILED OTHER ELEMENT NODAL FORCE EQULIBRIUM CALCULATIONS 
/COM                THIS CAN TAKE A VERY LONG TIME AND IS RECOMMENDED ONY FOR DEBUGING
/COM                CONVERGENCE PROBLEMS.  USE ARG2 TO LIMIT THE SETS OF GAP ELEMENTS
/COM                
/COM                THE RESULTS FROM OPTIONS 1 & 2 ME BE INVALID IF THE I OR J NODES
/COM                OF THE GAP ELEMENTS ARE INCLUDED IN ANY COUPLED SETS.
/COM
/COM        
/COM     ARG2 - STARTING  CONTAC52 ELEMENT REAL CONSTANT SET NUMBER
/COM     ARG3 - ENDING    CONTAC52 ELEMENT REAL CONSTANT SET NUMBER
/COM     ARG4 - INCREMENT CONTAC52 ELEMENT REAL CONSTANT SET NUMBER
/COM
/COM
/COM     ARG2 =  0   ALL CONTAC52 ELEMENTS WILL BE PROCESSED ( DEFAULT )
/COM
/COM          = -1   ONLY SELECTED CONTAC52 ELEMENTS WILL BE PROCESSED
/COM
/COM
/COM     ARG5 -  MODEL SECTOR SIZE IN DEG
/COM
/COM     ARG6 -  SUMMARIZED RESULTS SECTOR SIZE IN DEG
/COM
/COM
/COM  OUTPUTS :
/COM
/COM     CONTAC52 RESULTS SUMMARY WRITTEN TO FILE - contac52_fsum_node_real.lis
/COM
/COM     CONTAC52 RESULTS DATA WRITTEN TO FILE - contac52_fsum_node_real.out
/COM
/COM     CONTAC52 RESULTS SUMMARY WRITTEN TO FILE - contac52_fsum_node_real.csf
/COM
/COM     TERMINAL OUTPUT TO FILE - contac52_fsum_node_real.tmp ( DELEATED )
/COM
/COM
/COM  CREATED PARAMETERS : 
/COM 
/COM     CONTAC52 RESULTS SUMMARY ARRAY GAPSUM (NSET X 10) WERE NSET IS THE NUMBER OF CONTAC52
/COM
/COM     REAL CONSTANT SETS.  THE ARRAY DATA IS STORED IN SORTED ORDER BY REAL CONSTANT SET NUMBER.
/COM
/COM
/COM        GAPSUM(NSET,1)  =  REAL CONSTANT SET NUMBER
/COM
/COM        GAPSUM(NSET,2)  =  NUMBER OF ELEMENTS IN NSET
/COM
/COM        GAPSUM(NSET,3)  =  Mu COEFICIENT OF FRICTION ABS( SUM FS / SUM FN ) FOR 
/COM                           ELEMENTS WITH REAL CONSTANT SET NUMBER NSET
/COM
/COM        GAPSUM(NSET,4)  =  SUM OF NORMAL FORCES FOR ELEMENTS WITH REAL CONSTANT SET NUMBER NSET
/COM
/COM        GAPSUM(NSET,5)  =  SUM OF FRICTION FORCES FOR ELEMENTS WITH REAL CONSTANT SET NUMBER NSET
/COM
/COM        GAPSUM(NSET,6)  =  FX COMPONENT OF THE SUM OF CONTAC52 ELEMENT FORCES AT I NODES
/COM
/COM        GAPSUM(NSET,7)  =  FY COMPONENT OF THE SUM OF CONTAC52 ELEMENT FORCES AT I NODES
/COM
/COM        GAPSUM(NSET,8)  =  FZ COMPONENT OF THE SUM OF CONTAC52 ELEMENT FORCES AT I NODES
/COM
/COM        GAPSUM(NSET,9)  =  FX COMPONENT OF THE SUM OF CONTAC52 ELEMENT FORCES AT J NODES
/COM
/COM        GAPSUM(NSET,10) =  FY COMPONENT OF THE SUM OF CONTAC52 ELEMENT FORCES AT J NODES
/COM
/COM        GAPSUM(NSET,11) =  FZ COMPONENT OF THE SUM OF CONTAC52 ELEMENT FORCES AT J NODES
/COM
/COM     IF ARG1 = 1 OR 2
/COM 
/COM     CONTAC52 RESULTS SUMMARY ARRAY GAPSUM (NSET X 22)
/COM
/COM        GAPSUM(NSET,12) =  FX COMPONENT OF THE SUM OF ALL OTHER ELEMENT FORCES AT I NODES
/COM
/COM        GAPSUM(NSET,13) =  FY COMPONENT OF THE SUM OF ALL OTHER ELEMENT FORCES AT I NODES
/COM
/COM        GAPSUM(NSET,14) =  FZ COMPONENT OF THE SUM OF ALL OTHER ELEMENT FORCES AT I NODES
/COM
/COM        GAPSUM(NSET,15) =  FX COMPONENT OF THE SUM OF ALL OTHER ELEMENT FORCES AT J NODES
/COM
/COM        GAPSUM(NSET,16) =  FY COMPONENT OF THE SUM OF ALL OTHER ELEMENT FORCES AT J NODES
/COM
/COM        GAPSUM(NSET,17) =  FZ COMPONENT OF THE SUM OF ALL OTHER ELEMENT FORCES AT J NODES
/COM
/COM        GAPSUM(NSET,18) =  TOTAL NODE I FX DELTA - GAPSUM(NSET, 6) - GAPSUM(NSET,12)
/COM
/COM        GAPSUM(NSET,19) =  TOTAL NODE I FY DELTA - GAPSUM(NSET, 7) - GAPSUM(NSET,13)
/COM
/COM        GAPSUM(NSET,20) =  TOTAL NODE I FZ DELTA - GAPSUM(NSET, 8) - GAPSUM(NSET,14)
/COM
/COM        GAPSUM(NSET,21) =  TOTAL NODE J FX DELTA - GAPSUM(NSET, 9) - GAPSUM(NSET,15)
/COM
/COM        GAPSUM(NSET,22) =  TOTAL NODE J FY DELTA - GAPSUM(NSET,10) - GAPSUM(NSET,16)
/COM
/COM        GAPSUM(NSET,23) =  TOTAL NODE J FZ DELTA - GAPSUM(NSET,11) - GAPSUM(NSET,17)
/COM 
/COM 
/COM     CONTAC52 RESULTS ARRAY GAPIJ ( NX6 ) WERE N IS THE NUMBER OF CONTAC52 ELEMENTS.
/COM
/COM     THE ARRAY DATA IS STORED IN SORTED ORDER BY REAL CONSTANT SET NUMBER.
/COM 
/COM        GAPIJ(N,1)  =  CONTAC52 ELEMENT REAL CONSTANT SET NUMBER
/COM
/COM        GAPIJ(N,2)  =  CONTAC52 ELEMENT NUMBER N
/COM
/COM        GAPIJ(N,3)  =  CONTAC52 ELEMENT N NODE I
/COM
/COM        GAPIJ(N,4)  =  CONTAC52 ELEMENT N NODE J
/COM
/COM        GAPIJ(N,5)  =  CONTAC52 ELEMENT N NORMAL FORCE
/COM
/COM        GAPIJ(N,6)  =  CONTAC52 ELEMENT N FRICTION FORCE
/COM
/COM     IF ARG1 = 1 OR 2
/COM 
/COM     CONTAC52 RESULTS ARRAY GAPIJ ( NX18 )
/COM
/COM        GAPIJ(N,7)  =  CONTAC52 ELEMENT N NODE I FX FROM CONTAC52 ELEMENT
/COM
/COM        GAPIJ(N,8)  =  CONTAC52 ELEMENT N NODE I FY FROM CONTAC52 ELEMENT
/COM
/COM        GAPIJ(N,9)  =  CONTAC52 ELEMENT N NODE I FZ FROM CONTAC52 ELEMENT
/COM
/COM        GAPIJ(N,10) =  CONTAC52 ELEMENT N NODE J FX FROM CONTAC52 ELEMENT
/COM
/COM        GAPIJ(N,11) =  CONTAC52 ELEMENT N NODE J FY FROM CONTAC52 ELEMENT
/COM
/COM        GAPIJ(N,12) =  CONTAC52 ELEMENT N NODE J FZ FROM CONTAC52 ELEMENT
/COM
/COM        GAPIJ(N,13) =  CONTAC52 ELEMENT N NODE I FX FROM ALL OTHER ELEMENTS
/COM
/COM        GAPIJ(N,14) =  CONTAC52 ELEMENT N NODE I FY FROM ALL OTHER ELEMENTS
/COM
/COM        GAPIJ(N,15) =  CONTAC52 ELEMENT N NODE I FZ FROM ALL OTHER ELEMENTS
/COM
/COM        GAPIJ(N,16) =  CONTAC52 ELEMENT N NODE J FX FROM ALL OTHER ELEMENTS
/COM
/COM        GAPIJ(N,17) =  CONTAC52 ELEMENT N NODE J FY FROM ALL OTHER ELEMENTS
/COM
/COM        GAPIJ(N,18) =  CONTAC52 ELEMENT N NODE J FZ FROM ALL OTHER ELEMENTS
/COM
/COM        GAPIJ(N,19) =  CONTAC52 ELEMENT N NODE I FX DELTA - GAPIJ(N, 7) - GAPSUM(N,13)
/COM
/COM        GAPIJ(N,20) =  CONTAC52 ELEMENT N NODE I FY DELTA - GAPIJ(N, 8) - GAPSUM(N,14)
/COM
/COM        GAPIJ(N,21) =  CONTAC52 ELEMENT N NODE I FZ DELTA - GAPIJ(N, 9) - GAPSUM(N,15) 
/COM
/COM        GAPIJ(N,22) =  CONTAC52 ELEMENT N NODE J FX DELTA - GAPIJ(N,10) - GAPSUM(N,16)
/COM
/COM        GAPIJ(N,23) =  CONTAC52 ELEMENT N NODE J FY DELTA - GAPIJ(N,11) - GAPSUM(N,17) 
/COM
/COM        GAPIJ(N,24) =  CONTAC52 ELEMENT N NODE J FZ DELTA - GAPIJ(N,12) - GAPSUM(N,18) 
/COM
/COM
/COM
/COM
/COM   COMPONENT NAMES USED :
/COM
/COM
/COM        TMPELEM - ELEMENTS CURENTLY SELECTED ( DELEATED )
/COM
/COM        TMPNODE - NODES    CURENTLY SELECTED ( DELEATED )
/COM
/COM
/COM        TMPGAP1 - CONTAC52 GAP ELEMENTS      ( DELEATED )
/COM
/COM        TMPGAP2 - CONTAC52 GAP ELEMENTS      ( DELEATED )
/COM
/COM         
/COM        TMPNODEI - CONTAC52 GAP ELEM I NODES ( DELEATED )
/COM
!  
!     INTERNAL SCALAR VARIABLES :
!  
!        AR99 - I       =  DO LOOP COUNTER
!  
!        AR98 - NUMGAP  =  TOTAL NUMBER OF GAP ELEMENTS
!  
!        AR97 - ELNUM   =  NUMBER OF GAP ELEMENTS SELECTED
!  
!        AR96 - ELMIN   =  MINUMUM SELECTED GAP ELEMENT NUMBER
!  
!        AR95 - NUMSETS =  NUMBER OF GAP ELEMENT REAL CONSTANT SETS
!  
!        AR94 - NUMEL   =  ELEMENT COUNTER
!  
!        AR93 - J       =  DO LOOP COUNTER
!  
!        AR92 - MINELI  =  MINUMUM ELEMENT INDEX NUMBER FOR REAL CONSTANT SET
!  
!        AR91 - MAXELI  =  MAXIMUM ELEMENT INDEX NUMBER FOR REAL CONSTANT SET
!  
!        AR90 - RSYS    =  RESULTS COORDINATE SYSTEM BEFORE & AFTER EXECUTON
!  
!        AR89 - NUMOUT  =  NUMBER OF DATA SET WRITTEN PER PAGE
!  
!        AR88 - BCUX    =  NODAL BC UX 
!  
!        AR87 - BCUY    =  NODAL BC UY
!  
!        AR86 - BCUZ    =  NODAL BC UZ 
!  
!        AR85 - FBCX    =  NODAL REACTION FORCE FX 
!  
!        AR84 - FBCY    =  NODAL REACTION FORCE FY
!  
!        AR83 - FBCZ    =  NODAL REACTION FORCE FZ
!  
!        AR82 - MAXNODE =  MAXIMUM NODE NUMBER IN MODEL
!  
!        AR81 - NUMBCND =  NUMBER OF NODES WITH DISPLACMENT BC
!  
!        AR78 - NSECT   =  ARG6 / ARG5
!  
!        AR77 - ARSYS   =  ACTIVE RESULTS COORDINATE SYSTEM DURING EXECUTON
!  
!        AR76 - NSET    =  NUMBER OF DATA SETS 11 or 23
!  
!  
!     INTERNAL ARRAY VARIABLES :
!  
!        TMPDATA1      -  UNSORTED GAP ELEEMENT DATA
!  
!        TMPDATA1(I,1) =  GAP ELEMENT REAL CONSTANT SET NUMBER
!  
!        TMPDATA1(I,2) =  NUMBER OF GAP ELEMENTS
!  
!  
!        TMPDATA2      -  UNSORTED GAP ELEEMENT DATA
!  
!        TMPTL         -  ANALYSIS TITLE
!  
!  
/NOPR
!   *get,TSTART,ACTIVE,,TIME,CPU
!  
*IF,ARG1,GT,2,:STOP
*IF,ARG1,LT,0,:STOP
!  
!  
/OUTPUT,contac52_fsum_node_real,tmp    ! DUMP TERMINAL OUTPUT TO TMP FILE
!
*GET,AR90,ACTIVE,,RSYS           ! GET ACTIVE RESULTS COORD SYSTEM
!
*IF,ARG1,NE,0,THEN
RSYS,-1
AR77=-1
*ELSE
AR77=AR90
*ENDIF
!
!
CM,TMPELEM,ELEM
CM,TMPNODE,NODE
!
!
*IF,ARG2,EQ,0,THEN               ! SELECT CONTAC52 ELEMENTS
!
ESEL,S,ENAME,,CONTAC52           ! ARG2 = 0  SELECT ALL CONTAC52 ELEMENTS
!
*ELSEIF,ARG2,EQ,-1,THEN
!
ESEL,R,ENAME,,CONTAC52           ! ARG2 = -1 SELECT CONTAC52 ELEMENTS ALREADY SELECTED
!
*ELSE
!
ESEL,S,ENAME,,CONTAC52           ! SELECT CONTAC52 ELEMENTS BY REAL CONSTANT
ESEL,R,REAL,,ARG2,ARG3,ARG4
!
*ENDIF
!
!
ETABLE,FN,SMISC,1                ! STORE ETABLE RESULTS NORMAL FORCE
ETABLE,FS,SMISC,2                ! STORE ETABLE RESULTS FRICTION FORCE
!
CM,TMPGAP1,ELEM
CM,TMPGAP2,ELEM
!
*GET,AR98,ELEM,,COUNT            ! TOTAL NUMBER OF CONTAC52 ELEMENTS TO BE PROCESSED
*IF,AR98,EQ,0,:STOP
!
*DIM,TMPDATA1,ARRAY,AR98,2
!
*DO,AR99,1,AR98,1
CMSEL,S,TMPGAP1
*GET,AR97,ELEM,,COUNT
*IF,AR97,EQ,0,*EXIT
*GET,AR96,ELEM,,NUM,MIN
*GET,TMPDATA1(AR99,1),ELEM,AR96,ATTR,REAL
ESEL,R,REAL,,TMPDATA1(AR99,1)
*GET,TMPDATA1(AR99,2),ELEM,,COUNT
CMSEL,S,TMPGAP1
ESEL,U,REAL,,TMPDATA1(AR99,1)
CM,TMPGAP1,ELEM
*ENDDO  ! AR99
!
AR95=AR99-1                    ! TOTAL NUMBER OF CONTAC52 ELEMENT SETS TO BE PROCESSED
!
*DEL,GAPSUM
*DEL,GAPIJ
*DEL,TMPDATA2
!
!
*IF,ARG1,EQ,2,THEN
!
!
*DIM,GAPIJ,ARRAY,(AR98+1),24,0
*DIM,GAPSUM,ARRAY,AR95,23
!
CMSEL,S,TMPGAP2
NELEM
*GET,AR82,MXND              ! AR82 - MAX NODE NUMBER
*DIM,TMPBCND,ARRAY,AR82
!
NSEL,R,D,U,-999,999 
*GET,AR81,NODE,,COUNT       ! AR81 - NUMBER OF NODES WITH DISP. BC
!
*IF,AR81,NE,0,THEN
!
AR80=0
*DO,AR79,1,AR81,1
AR80=NDNEXT(AR80)
TMPBCND(AR80)=1
!
*ENDDO  ! AR79
*ENDIF
!
!
*ELSEIF,ARG1,EQ,1,THEN
!
*DIM,GAPIJ,ARRAY,(AR98+1),6,0
*DIM,GAPSUM,ARRAY,AR95,23
!
*ELSE
!
*DIM,GAPIJ,ARRAY,(AR98+1),6,0
*DIM,GAPSUM,ARRAY,AR95,11
!
*ENDIF
!
!
!
*DIM,TMPDATA2,ARRAY,AR95
*VCOL,2
*VLEN,AR95
*MFUN,GAPSUM(1,1),COPY,TMPDATA1(1,1)
*MOPER,TMPDATA2(1,1),GAPSUM(1,1),SORT,TMPDATA1(1,1)
!
!
!
AR94=1
!
*DO,AR99,1,AR95,1
!
!
!
CMSEL,S,TMPGAP2
!
ESEL,R,REAL,,GAPSUM(AR99,1)
NELEM
PRETABLE
SSUM
*GET,GAPSUM(AR99,4),SSUM,,ITEM,FN
*GET,GAPSUM(AR99,5),SSUM,,ITEM,FS
!
!
*IF,GAPSUM(AR99,4),NE,0,THEN
!
GAPSUM(AR99,3)=ABS(GAPSUM(AR99,5)/GAPSUM(AR99,4))
!
*ELSE
!
GAPSUM(AR99,3)=0
!
*ENDIF
!
*GET,GAPIJ(AR94,2),ELEM,,NUM,MIN
!
AR92=AR94  ! STARTING ELEM INDEX FOR THIS REAL CONSTANT SET
!
*DO,AR93,1,GAPSUM(AR99,2),1
GAPIJ(AR94,1)=GAPSUM(AR99,1)
*GET,GAPIJ(AR94,3),ELEM,GAPIJ(AR94,2),NODE,1
*GET,GAPIJ(AR94,4),ELEM,GAPIJ(AR94,2),NODE,2
*GET,GAPIJ(AR94,5),ELEM,GAPIJ(AR94,2),ETAB,FN
*GET,GAPIJ(AR94,6),ELEM,GAPIJ(AR94,2),ETAB,FS
!
!
!
*IF,ARG1,EQ,2,THEN
!
!  PREFORM DETAILED GAP ELEMENT ANALYSIS 
!
!  SELECT I NODE
!
NSEL,S,NODE,,GAPIJ(AR94,3)
SPOINT,GAPIJ(AR92,3)
FSUM,RSYS
!
*GET,GAPIJ(AR94,7),FSUM,,ITEM,FX
*GET,GAPIJ(AR94,8),FSUM,,ITEM,FY
*GET,GAPIJ(AR94,9),FSUM,,ITEM,FZ
GAPSUM(AR99, 6)=GAPSUM(AR99, 6)+GAPIJ(AR94,7)
GAPSUM(AR99, 7)=GAPSUM(AR99, 7)+GAPIJ(AR94,8)
GAPSUM(AR99, 8)=GAPSUM(AR99, 8)+GAPIJ(AR94,9)
!
!  SELECT J NODE
!
NSEL,S,NODE,,GAPIJ(AR94,4)
SPOINT,GAPIJ(AR92,4)
FSUM,RSYS
!
!
*GET,GAPIJ(AR94,10),FSUM,,ITEM,FX
*GET,GAPIJ(AR94,11),FSUM,,ITEM,FY
*GET,GAPIJ(AR94,12),FSUM,,ITEM,FZ
GAPSUM(AR99, 9)=GAPSUM(AR99, 9)+GAPIJ(AR94,10)
GAPSUM(AR99,10)=GAPSUM(AR99,10)+GAPIJ(AR94,11)
GAPSUM(AR99,11)=GAPSUM(AR99,11)+GAPIJ(AR94,12)
!
!
*ENDIF
!
!
AR91=AR94 ! ENDING ELEM INDEX FOR THIS REAL CONSTANT SET
!
*GET,GAPIJ(AR94+1,2),ELEM,GAPIJ(AR94,2),NXTH
AR94=AR94+1
!
*ENDDO  ! AR93
!
!
*IF,ARG1,EQ,2,THEN
!
!  PREFORM DETAILED OTHER ELEMENT ANALYSIS 
!
!  THIS REAL CONSTANT SET IS FROM ELEM INDEX AR92 TO AR91
!
!
*DO,AR93,AR92,AR91,1
!
!
!  SELECT I NODE
!
NSEL,S,NODE,,GAPIJ(AR93,3)     
!
ENODE
ESEL,U,ELEM,,GAPIJ(AR93,2)           ! 
!
SPOINT,GAPIJ(AR92,3)
FSUM,RSYS
!
!
*IF,TMPBCND(GAPIJ(AR93,3)),EQ,1,THEN
!
!
*GET,AR88,NODE,GAPIJ(AR93,3),D,UX
*GET,AR87,NODE,GAPIJ(AR93,3),D,UY
*GET,AR86,NODE,GAPIJ(AR93,3),D,UZ
!
*IF,ABS(AR88),LE,1E6,THEN
GAPIJ(AR93,13)=0
*ELSE
*GET,GAPIJ(AR93,13),FSUM,,ITEM,FX
*ENDIF
!
*IF,ABS(AR87),LE,1E6,THEN
GAPIJ(AR93,14)=0
*ELSE
*GET,GAPIJ(AR93,14),FSUM,,ITEM,FY
*ENDIF
!
*IF,ABS(AR86),LE,1E6,THEN
GAPIJ(AR93,15)=0
*ELSE
*GET,GAPIJ(AR93,15),FSUM,,ITEM,FZ
*ENDIF
!
!
*ELSE
!
!
*GET,GAPIJ(AR93,13),FSUM,,ITEM,FX
*GET,GAPIJ(AR93,14),FSUM,,ITEM,FY
*GET,GAPIJ(AR93,15),FSUM,,ITEM,FZ
!
!
*ENDIF
!
!
GAPIJ(AR93,19)=GAPIJ(AR93, 7)+GAPIJ(AR93,13)
GAPIJ(AR93,20)=GAPIJ(AR93, 8)+GAPIJ(AR93,14)
GAPIJ(AR93,21)=GAPIJ(AR93, 9)+GAPIJ(AR93,15)
!
GAPSUM(AR99,12)=GAPSUM(AR99,12)+GAPIJ(AR93,13)
GAPSUM(AR99,13)=GAPSUM(AR99,13)+GAPIJ(AR93,14)
GAPSUM(AR99,14)=GAPSUM(AR99,14)+GAPIJ(AR93,15)
!
!
!
!  SELECT J NODE
!
NSEL,S,NODE,,GAPIJ(AR93,4)
ENODE
ESEL,U,ELEM,,GAPIJ(AR93,2)           ! 
!
SPOINT,GAPIJ(AR92,4)
FSUM,RSYS
!
!
*IF,TMPBCND(GAPIJ(AR93,4)),EQ,1,THEN
!
!
*GET,AR88,NODE,GAPIJ(AR93,4),D,UX
*GET,AR87,NODE,GAPIJ(AR93,4),D,UY
*GET,AR86,NODE,GAPIJ(AR93,4),D,UZ
!
*IF,ABS(AR88),LE,1E6,THEN
GAPIJ(AR93,16)=0
*ELSE
*GET,GAPIJ(AR93,16),FSUM,,ITEM,FX
*ENDIF
!
*IF,ABS(AR87),LE,1E6,THEN
GAPIJ(AR93,17)=0
*ELSE
*GET,GAPIJ(AR93,17),FSUM,,ITEM,FY
*ENDIF
!
*IF,ABS(AR86),LE,1E6,THEN
GAPIJ(AR93,18)=0
*ELSE
*GET,GAPIJ(AR93,18),FSUM,,ITEM,FZ
*ENDIF
!
!
*ELSE
!
!
*GET,GAPIJ(AR93,16),FSUM,,ITEM,FX
*GET,GAPIJ(AR93,17),FSUM,,ITEM,FY
*GET,GAPIJ(AR93,18),FSUM,,ITEM,FZ
!
!
*ENDIF
!
GAPIJ(AR93,22)=GAPIJ(AR93,10)+GAPIJ(AR93,16)
GAPIJ(AR93,23)=GAPIJ(AR93,11)+GAPIJ(AR93,17)
GAPIJ(AR93,24)=GAPIJ(AR93,12)+GAPIJ(AR93,18)
!
GAPSUM(AR99,15)=GAPSUM(AR99,15)+GAPIJ(AR93,16)
GAPSUM(AR99,16)=GAPSUM(AR99,16)+GAPIJ(AR93,17)
GAPSUM(AR99,17)=GAPSUM(AR99,17)+GAPIJ(AR93,18)
!
!
*ENDDO ! AR93
!
!
*ELSE
!
!
!  PREFORM REDUCED GAP ANALYSIS 
!
!  THIS REAL CONSTANT SET IS FROM ELEM INDEX AR92 TO AR91
!
!
!  SELECT ALL I NODES
!
ESEL,S,REAL,,GAPSUM(AR99,1)
NELEM
!
!
*DO,AR93,AR92,AR91,1
!
NSEL,U,NODE,,GAPIJ(AR93,4)
!
*ENDDO  ! AR93
!
CM,TMPNODEI,NODE
!
!
SPOINT,GAPIJ(AR92,3)
FSUM,RSYS
!
*GET,GAPSUM(AR99,6),FSUM,,ITEM,FX
*GET,GAPSUM(AR99,7),FSUM,,ITEM,FY
*GET,GAPSUM(AR99,8),FSUM,,ITEM,FZ
!
!
*IF,ARG1,EQ,1,THEN
!
ENODE
ESEL,U,REAL,,GAPSUM(AR99,1)
FSUM,RSYS
!
*GET,GAPSUM(AR99,12),FSUM,,ITEM,FX
*GET,GAPSUM(AR99,13),FSUM,,ITEM,FY
*GET,GAPSUM(AR99,14),FSUM,,ITEM,FZ
!
!
NSEL,R,D,U,-999,999 
*GET,AR81,NODE,,COUNT
!
*IF,AR81,NE,0,THEN
!
*DIM,TMPBCND,ARRAY,AR81
!
AR80=0
*DO,AR79,1,AR81,1
AR80=NDNEXT(AR80)
TMPBCND(AR79)=AR80
*ENDDO
!
*DO,AR79,1,AR81,1
!
NSEL,S,NODE,,TMPBCND(AR79)
ENODE
ESEL,U,REAL,,GAPSUM(AR99,1)
FSUM,RSYS
!
*GET,AR85,FSUM,,ITEM,FX
*GET,AR84,FSUM,,ITEM,FY
*GET,AR83,FSUM,,ITEM,FZ
!
*GET,AR88,NODE,TMPBCND(AR79),D,UX
*GET,AR87,NODE,TMPBCND(AR79),D,UY
*GET,AR86,NODE,TMPBCND(AR79),D,UZ
!
*IF,ABS(AR88),LE,1E6,THEN
GAPSUM(AR99,12)=GAPSUM(AR99,12)-AR85
*ENDIF
!
*IF,ABS(AR87),LE,1E6,THEN
GAPSUM(AR99,13)=GAPSUM(AR99,13)-AR84
*ENDIF
!
*IF,ABS(AR86),LE,1E6,THEN
GAPSUM(AR99,14)=GAPSUM(AR99,14)-AR83
*ENDIF
!
!
*ENDDO  ! AR79
!
*DEL,TMPBCND
!
*ENDIF   ! AR81 
!
*ENDIF   ! ARG1 = 1
!
!
!
!  SELECT ALL J NODES
!
ESEL,S,REAL,,GAPSUM(AR99,1)
NELEM
!
CMSEL,U,TMPNODEI
!
SPOINT,GAPIJ(AR92,4)
FSUM,RSYS
!
*GET,GAPSUM(AR99, 9),FSUM,,ITEM,FX
*GET,GAPSUM(AR99,10),FSUM,,ITEM,FY
*GET,GAPSUM(AR99,11),FSUM,,ITEM,FZ
!
!
*IF,ARG1,EQ,1,THEN
!
!
ENODE
ESEL,U,REAL,,GAPSUM(AR99,1)
FSUM,RSYS
!
*GET,GAPSUM(AR99,15),FSUM,,ITEM,FX
*GET,GAPSUM(AR99,16),FSUM,,ITEM,FY
*GET,GAPSUM(AR99,17),FSUM,,ITEM,FZ
!
!
NSEL,R,D,U,-999,999 
*GET,AR81,NODE,,COUNT
!
*IF,AR81,NE,0,THEN
!
*DIM,TMPBCND,ARRAY,AR81
!
AR80=0
*DO,AR79,1,AR81,1
AR80=NDNEXT(AR80)
TMPBCND(AR79)=AR80
*ENDDO
!
*DO,AR79,1,AR81,1
!
NSEL,S,NODE,,TMPBCND(AR79)
ENODE
ESEL,U,REAL,,GAPSUM(AR99,1)
FSUM,RSYS
!
*GET,AR85,FSUM,,ITEM,FX
*GET,AR84,FSUM,,ITEM,FY
*GET,AR83,FSUM,,ITEM,FZ
!
*GET,AR88,NODE,TMPBCND(AR79),D,UX
*GET,AR87,NODE,TMPBCND(AR79),D,UY
*GET,AR86,NODE,TMPBCND(AR79),D,UZ
!
*IF,ABS(AR88),LE,1E6,THEN
GAPSUM(AR99,15)=GAPSUM(AR99,15)-AR85
*ENDIF
!
*IF,ABS(AR87),LE,1E6,THEN
GAPSUM(AR99,16)=GAPSUM(AR99,16)-AR84
*ENDIF
!
*IF,ABS(AR86),LE,1E6,THEN
GAPSUM(AR99,17)=GAPSUM(AR99,17)-AR83
*ENDIF
!
!
*ENDDO  ! AR79
!
*DEL,TMPBCND
!
*ENDIF   ! AR81
!
*ENDIF   ! ARG1 = 1
!
!
!
*ENDIF
!
!
*IF,ARG1,NE,0,THEN
!
GAPSUM(AR99,18)=GAPSUM(AR99,12)+GAPSUM(AR99, 6)
GAPSUM(AR99,19)=GAPSUM(AR99,13)+GAPSUM(AR99, 7)
GAPSUM(AR99,20)=GAPSUM(AR99,14)+GAPSUM(AR99, 8)
GAPSUM(AR99,21)=GAPSUM(AR99,15)+GAPSUM(AR99, 9)
GAPSUM(AR99,22)=GAPSUM(AR99,16)+GAPSUM(AR99,10)
GAPSUM(AR99,23)=GAPSUM(AR99,17)+GAPSUM(AR99,11)
!
*ENDIF
!
!
*ENDDO  ! AR99
!
!
!
*DIM,TMPTL,CHAR,10
*DO,AR99,1,10,1
*GET,TMPTL(AR99),ACTIVE,,TITLE,0,START,((AR99-1)*8+1)
*ENDDO   ! AR99
!
!
*CFOPEN,contac52_fsum_node_real,out
!
!
AR94=1
*DO,AR99,1,AR95,1
!
!
*VLEN,1
*VWRITE,TMPTL(1),TMPTL(2),TMPTL(3),TMPTL(4),TMPTL(5),TMPTL(6),TMPTL(7),TMPTL(8),TMPTL(9),TMPTL(10)
('1',//,15X,10(A8))
!
*VWRITE
(//,28X,'CONTAC52 RESULTS DATA')
!
!
*VWRITE
(//,4X,'REAL',2X,'ELEM NUM',4X,'NODE I',4X,'NODE J',6X,'NORMAL FORCE',4X,'FRICRION FORCE',/)
!
*VLEN,GAPSUM(AR99,2)
*VWRITE,GAPIJ(AR94,1),GAPIJ(AR94,2),GAPIJ(AR94,3),GAPIJ(AR94,4),GAPIJ(AR94,5),GAPIJ(AR94,6)
(I8,2X,I8,2X,I8,2X,I8,2(2X,F16.1))
!
*VWRITE
(/,4X,'REAL',2X,'NUM ELEM',26X,'TOTAL NORMAL',4X,'TOTAL FRICTION')
!
*VLEN,1
*VWRITE,GAPSUM(AR99,1),GAPSUM(AR99,2),GAPSUM(AR99,4),GAPSUM(AR99,5)
(/,I8,2X,I8,20X,2(2X,F16.1))
!
!
*IF,ARG1,EQ,2,THEN
!
*IF,AR77,NE,-1,THEN
*VWRITE,AR77
(//,34X,'RESULTS ARE IN COORDINATE SYSTEM',1X,I3)
*ELSE
*VWRITE
(//,32X,'RESULTS ARE IN THE NODAL COORDINATE SYSTEM')
*ENDIF
!
*VWRITE
(//,22X,'<-------   CONTAC52 NODE I    ------->',2X,'<-------    OTHER NODE I      ------->')
*VWRITE
(2X,'ELEM NUM',4X,'NODE I',2(12X,'FX',11X,'FY',11X,'FZ'),/)
!
*VLEN,GAPSUM(AR99,2)
*VWRITE,GAPIJ(AR94,2),GAPIJ(AR94,3),GAPIJ(AR94,7),GAPIJ(AR94,8),GAPIJ(AR94,9),GAPIJ(AR94,13),GAPIJ(AR94,14),GAPIJ(AR94,15)
(2X,I8,2X,I8,2(2X,F12.1,1X,F12.1,1X,F12.1))
!
*VLEN,1
*VWRITE,GAPSUM(AR99,6),GAPSUM(AR99,7),GAPSUM(AR99,8),GAPSUM(AR99,12),GAPSUM(AR99,13),GAPSUM(AR99,14)
(/,15X,'TOTAL',2(2X,F12.1,1X,F12.1,1X,F12.1))
!
!
*IF,AR77,NE,-1,THEN
*VWRITE,AR77
(//,34X,'RESULTS ARE IN COORDINATE SYSTEM',1X,I3)
*ELSE
*VWRITE
(//,32X,'RESULTS ARE IN THE NODAL COORDINATE SYSTEM')
*ENDIF
!
*VWRITE
(//,22X,'<-------   CONTAC52 NODE J    ------->',2X,'<-------    OTHER NODE J      ------->')
*VWRITE
(2X,'ELEM NUM',4X,'NODE J',2(12X,'FX',11X,'FY',11X,'FZ'),/)
!
*VLEN,GAPSUM(AR99,2)
*VWRITE,GAPIJ(AR94,2),GAPIJ(AR94,4),GAPIJ(AR94,10),GAPIJ(AR94,11),GAPIJ(AR94,12),GAPIJ(AR94,16),GAPIJ(AR94,17),GAPIJ(AR94,18)
(2X,I8,2X,I8,2(2X,F12.1,1X,F12.1,1X,F12.1))
!
*VLEN,1
*VWRITE,GAPSUM(AR99,9),GAPSUM(AR99,10),GAPSUM(AR99,11),GAPSUM(AR99,15),GAPSUM(AR99,16),GAPSUM(AR99,17)
(/,15X,'TOTAL',2(2X,F12.1,1X,F12.1,1X,F12.1))
!
!
*IF,AR77,NE,-1,THEN
*VWRITE,AR77
(//,34X,'RESULTS ARE IN COORDINATE SYSTEM',1X,I3)
*ELSE
*VWRITE
(//,32X,'RESULTS ARE IN THE NODAL COORDINATE SYSTEM')
*ENDIF
!
*VWRITE
(//,24X,'<-------    DELTA NODE I    ------->',2X,'<-------    DELTA NODE J      ------->')
*VWRITE
(4X,'REAL',2X,'ELEM NUM',2X,2(12X,'FX',11X,'FY',11X,'FZ'),/)
!
*VLEN,GAPSUM(AR99,2)
*VWRITE,GAPIJ(AR94,1),GAPIJ(AR94,2),GAPIJ(AR94,19),GAPIJ(AR94,20),GAPIJ(AR94,21),GAPIJ(AR94,22),GAPIJ(AR94,23),GAPIJ(AR94,24)
(I8,2X,I8,2X,2(2X,F12.1,1X,F12.1,1X,F12.1))
!
*VLEN,1
*VWRITE,GAPSUM(AR99,18),GAPSUM(AR99,19),GAPSUM(AR99,20),GAPSUM(AR99,21),GAPSUM(AR99,22),GAPSUM(AR99,23)
(/,9X,'TOTAL DELTA',2(2X,F12.1,1X,F12.1,1X,F12.1))
!
*ENDIF
!
!
AR94=AR94+GAPSUM(AR99,2)
!
!
*ENDDO
!
*CFCLOSE
!
!
!
!
!
*IF,ARG5,NE,0,THEN
!
AR78=ARG6/ARG5
!
*ENDIF
!
!
*IF,ARG1,EQ,0,THEN
AR76=11
*ELSE
AR76=23
*ENDIF
!
!
*IF,AR78,NE,0,THEN
!
*DO,AR99,1,AR95,1
*DO,AR93,4,AR76,1
GAPSUM(AR99,AR93)=GAPSUM(AR99,AR93)*AR78
*ENDDO
*ENDDO
!
*ENDIF
!
!
!
!  WRITE CONTAC52 RESULTS SUMMARY TO FILE
!
!
*CFOPEN,contac52_fsum_node_real,lis
!
!
*IF,ARG1,EQ,1,THEN    !  NUMBER OF DATA SET WRITTEN PER PAGE
AR89=6
*ELSE
AR89=20
*ENDIF
!
!
*DO,AR99,1,AR95,1
!
!
*IF,MOD(AR99-1,AR89),EQ,0,THEN
!
*VLEN,1
*VWRITE,TMPTL(1),TMPTL(2),TMPTL(3),TMPTL(4),TMPTL(5),TMPTL(6),TMPTL(7),TMPTL(8),TMPTL(9),TMPTL(10)
('1',//,30X,10(A8))
!
*VWRITE
(//,37X,'CONTAC52 RESULTS DATA SUMMARY')
!
*IF,AR77,NE,-1,THEN
*VWRITE,AR77
(/,37X,'RESULTS ARE IN COORDINATE SYSTEM',1X,I3)
*ELSE
*VWRITE
(/,35X,'RESULTS ARE IN THE NODAL COORDINATE SYSTEM')
*ENDIF
!
!
*IF,ARG5,NE,0,THEN
*VWRITE,ARG5
(/,37X,'THE MODEL WAS OF A ',F7.3,' DEG SECTOR')
*ENDIF
!
!
*IF,AR78,NE,0,THEN
*VWRITE,ARG6
(/,37X,'RESULTS ARE FOR A  ',F7.3,' DEG SECTOR')
*ENDIF
!
!
!
*VWRITE
(///,14X,'TOTAL',5X,'TOTAL',30X,'<---   TOTAL NODE I   --->',7X,'<---   TOTAL NODE J   --->')
!
*VWRITE
(4X,'REAL',6X,'NORMAL',4X,'FRICTION',3X,'  Mu   ',3X,'ENAME ',2(9X,'Fx',9X,'Fy',9X,'Fz'))
!
*ENDIF
!
I=AR99
!
*VLEN,1
*VWRITE,GAPSUM(I,1),GAPSUM(I,4),GAPSUM(I,5),GAPSUM(I,3),GAPSUM(I,6),GAPSUM(I,7),GAPSUM(I,8),GAPSUM(I,9),GAPSUM(I,10),GAPSUM(I,11)
(/,I8,2(2X,F10.1),3X,F6.4,3X,'CONTAC52',6(1X,F10.1))
!
*IF,ARG1,NE,0,THEN
!
*VLEN,1
*VWRITE,GAPSUM(I,12),GAPSUM(I,13),GAPSUM(I,14),GAPSUM(I,15),GAPSUM(I,16),GAPSUM(I,17)
(/,44X,'OTHER   ',6(1X,F10.1))
!
*VLEN,1
*VWRITE,GAPSUM(I,18),GAPSUM(I,19),GAPSUM(I,20),GAPSUM(I,21),GAPSUM(I,22),GAPSUM(I,23)
(/,44X,'DELTA   ',6(1X,F10.1)//)
!
*ENDIF
!
*ENDDO
!
*CFCLOSE
!
!
!  WRITE CONTAC52 RESULTS SUMMARY TO COMMA SEPERATED FILE
!
!
*CFOPEN,contac52_fsum_node_real,csf
!
!
*VLEN,1
*VWRITE,TMPTL(1),TMPTL(2),TMPTL(3),TMPTL(4),TMPTL(5),TMPTL(6),TMPTL(7),TMPTL(8),TMPTL(9),TMPTL(10)
(//,30X,10(A8))
!
!
*VWRITE
(//,37X,'CONTAC52 RESULTS DATA SUMMARY')
!
*IF,AR77,NE,-1,THEN
*VWRITE,AR77
(/,37X,'RESULTS ARE IN COORDINATE SYSTEM',1X,I3)
*ELSE
*VWRITE
(/,35X,'RESULTS ARE IN THE NODAL COORDINATE SYSTEM')
*ENDIF
!
!
*IF,ARG5,NE,0,THEN
*VWRITE,ARG5
(/,37X,'THE MODEL WAS OF A ',F7.3,' DEG SECTOR')
*ENDIF
!
!
*IF,AR78,NE,0,THEN
*VWRITE,ARG6
(/,37X,'RESULTS ARE FOR A  ',F7.3,' DEG SECTOR')
*ENDIF
!
!
!
*VWRITE
(///,',TOTAL,TOTAL,,,<---   TOTAL NODE I   --->,,,<---   TOTAL NODE J   --->,')
 
*VWRITE
('REAL,','NORMAL,','FRICTION,','Mu,','ENAME,',2('Fx,','Fy,','Fz,'))
!
!
*DO,AR99,1,AR95,1
!
I=AR99
!
*VLEN,1
*VWRITE,GAPSUM(I,1),GAPSUM(I,4),GAPSUM(I,5),GAPSUM(I,3),GAPSUM(I,6),GAPSUM(I,7),GAPSUM(I,8),GAPSUM(I,9),GAPSUM(I,10),GAPSUM(I,11)
(I8,',',2(F10.1,','),F6.4,',','CONTAC52,',6(F10.1,','))
!
*IF,ARG1,NE,0,THEN
!
*VLEN,1
*VWRITE,GAPSUM(I,12),GAPSUM(I,13),GAPSUM(I,14),GAPSUM(I,15),GAPSUM(I,16),GAPSUM(I,17)
(',,,,OTHER,',6(F10.1,','))
!
*VLEN,1
*VWRITE,GAPSUM(I,18),GAPSUM(I,19),GAPSUM(I,20),GAPSUM(I,21),GAPSUM(I,22),GAPSUM(I,23)
(',,,,DELTA,',6(F10.1,','))
!
*ENDIF
!
*ENDDO
!
*CFCLOSE
!
!
*DEL,TMPDATA1
*DEL,TMPDATA2
*DEL,TMPBCND
*DEL,TMPTL
*DEL,I
!
/OUTPUT
/GOPR
/COM
/COM     CONTAC52 RESULTS SUMMARY WRITTEN TO FILE - contac52_fsum_node_real.lis
/COM
/COM     CONTAC52 RESULTS DATA WRITTEN TO FILE - contac52_fsum_node_real.out
/COM
/COM     CONTAC52 RESULTS SUMMARY WRITTEN TO FILE - contac52_fsum_node_real.csf
/COM
/NOPR
!
:STOP
!
CMSEL,S,TMPNODE
CMSEL,S,TMPELEM
CMDEL,TMPNODE
CMDEL,TMPELEM
CMDEL,TMPGAP1
CMDEL,TMPGAP2
CMDEL,TMPNODEI
RSYS,AR90
/SYS,rm contac52_fsum_node_real.tmp
!
!   *get,TSTOP,ACTIVE,,TIME,CPU
!   TIMEDEL=TSTOP-TSTART
/GOPR
/EOF
