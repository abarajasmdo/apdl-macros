!
!   MACRO  PFACE-NDS:  MACRO WRITES OUT A FILE OF NODE PAIRS STARTING AT THE !   
!                      OUTERMOST FWD NODE ON THE PRESSURE FACE FWD TO AFT OUTER 
!                      TO INNER ALONG THE PRESSURE FACE.  FOR FRICGEN.
!
!   USAGE PFACE-NDS,NROWS,NCOLS  WHERE:NCOLS=# OF COLS  NROWS=# OF ROWS
!
x=
CM,ALL,NODE
CSYS,11
NROWS=ARG1
NCOLS=ARG2
*CFOPEN,NODELIST
*CFWRIT,,NROWS, ,NCOLS
!
! START LOOP THAT DOES ONE ROW AT A TIME
!
*DO,R,1,NROWS
!
!  DO LOOP TO SELECT OUTERMOST ROW OF NODES 
!
XOFF=.011
*GET,XMAX,NODE,,MXLOC,X
	*DO,N,1,10
	NSEL,R,LOC,X,XMAX-XOFF,XMAX
	*GET,NUM,NODE,,COUNT
	*IF,NUM,EQ,NCOLS,EXIT
	*IF,NUM,LT,NCOLS,THEN
	*MSG,WARN,NUM,R
	GOT %I NODES FOR ROW %I
	*ELSE
	*ENDIF
	XOFF=XOFF-.001
	*ENDDO
CM,ROW,NODE
CMSEL,S,ALL
CMSEL,U,ROW
CM,ALL,NODE
CMSEL,S,ROW
*GET,YMIN,NODE,,MNLOC,Y
NLAST=NODE(XMAX,YMIN,0)
NSEL,S,,,NLAST
ENODE
ESEL,R,STIF,,40
NELEM
*CFWRIT,,NDNEXT(NLAST), ,NLAST    !  BLADE TO DISK NODE OFFSET=40000
CMSEL,S,ROW
NSEL,U,,,NLAST
CM,ROW,NODE
!
!  DO THE REST OF THE COLUMN
!
	*DO,FWD,1,NCOLS-1
	ND=NNEAR(NLAST)
	NLAST=ND
	NSEL,S,,,NLAST
	ENODE
	ESEL,R,STIF,,40
	NELEM
	*CFWRIT,,NDNEXT(NLAST), ,NLAST    !  BLADE TO DISK NODE OFFSET=40000
	*msg,info,NDNEXT(NLAST),NLAST
	wrote nodes %I %I
	CMSEL,S,ROW
	NSEL,U,,,NLAST
	CM,ROW,NODE
	*ENDDO
CMSEL,S,ALL
*MSG,INFO,R
FINISHED ROW %I
*ENDDO
*CFCLOS
*MSG,INFO
NOTE:  DATA IS ON FILE NODEFILE	
NLAST=
ND=
YMIN=
NUM=
XMAX=
FWD=
R=
N=
