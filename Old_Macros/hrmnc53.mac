/bat
C*** BATCH FILE TO EVALUATE AND CHECK THE FOURIER SERIES
C*** OF A USER-SPECIFIED FUNCTION OF THETA.
C*** BILL BULAT 11/15/96

C*** ARRAYS PARAMETERS:
C*** 1) COEFF: FOURIER COEFFICIENTS
C*** 2) MODE: MODE NUMBERS (COEFFICIENTS ON THETA)
C*** 3) ISYM: (TERM TYPE IDENTIFIER: 1=COSINE, -1=SINE)
C*** 4) THETA: 
C*** 5) FNCTNIN: F(THETA) FOR WHICH FOURIER COEFFICIENTS ARE
C***    TO BE DETERMINED.
C*** 6) FNCTNOUT: ANSYS EVALUATION OF TRUCATED FOURIER SERIES
C***    (TO BE COMPARED WITH FNTNIN)

C*** TABLE ARRAYS:
C*** 1) THETAP, FINP, AND FOUTP: TABLE ARRAYS FILLED WITH SAME VALUES
C***    AS THETA, FNCTNIN, AND FNCTNOUT. TABLE ARRAYS PLOT IN ANSYS
C***    WITHOUT APPEARING AS "BAR CHART".

C*** PARAMETERS: EDIT TO SUIT
NMDS = 64     ! DESIRED NUMBER OF FOURIER SERIES MODES
NDATAPTS = 360 ! # OF DATA PTS DESCRIBING F(THETA)

C*** DIMENSION ARRAYS
*GET,DEFINED,PARM,COEFF,TYPE
*IF,DEFINED,NE,1,THEN
*DIM,COEFF,,2*NMDS+1
*ELSE
*GET,DEFINED,PARM,COEFF,DIM,X
*IF,DEFINED,NE,2*NMDS+1,THEN
*DIM,COEFF,,2*NMDS+1
*ENDIF
*ENDIF

*GET,DEFINED,PARM,MODE,TYPE
*IF,DEFINED,NE,1,THEN
*DIM,MODE,,2*NMDS+1
*ELSE
*GET,DEFINED,PARM,MODE,DIM,X
*IF,DEFINED,NE,2*NMDS+1,THEN
*DIM,MODE,,2*NMDS+1
*ENDIF
*ENDIF

*GET,DEFINED,PARM,ISYM,TYPE
*IF,DEFINED,NE,1,THEN
*DIM,ISYM,,2*NMDS+1
*ELSE
*GET,DEFINED,PARM,ISYM,DIM,X
*IF,DEFINED,NE,2*NMDS+1,THEN
*DIM,ISYM,,2*NMDS+1
*ENDIF
*ENDIF

*GET,DEFINED,PARM,THETA,TYPE
*IF,DEFINED,NE,1,THEN
*DIM,THETA,,NDATAPTS
*ELSE
*GET,DEFINED,PARM,THETA,DIM,X
*IF,DEFINED,NE,NDATAPTS,THEN
*DIM,THETA,,NDATAPTS
*ENDIF
*ENDIF

*GET,DEFINED,PARM,FNCTNIN,TYPE
*IF,DEFINED,NE,1,THEN
*DIM,FNCTNIN,,NDATAPTS
*ELSE
*GET,DEFINED,PARM,FNCTNIN,DIM,X
*IF,DEFINED,NE,NDATAPTS,THEN
*DIM,FNCTNIN,,NDATAPTS
*ENDIF
*ENDIF

*GET,DEFINED,PARM,FNCTNOUT,TYPE
*IF,DEFINED,NE,1,THEN
*DIM,FNCTNOUT,,NDATAPTS
*ELSE
*GET,DEFINED,PARM,FNCTNOUT,DIM,X
*IF,DEFINED,NE,NDATAPTS,THEN
*DIM,FNCTNOUT,,NDATAPTS
*ENDIF
*ENDIF

C*** DIMENSION PLOTTING ARRAYS
*GET,DEFINED,PARM,THETAP,TYPE
*IF,DEFINED,NE,2,THEN
*DIM,THETAP,TABLE,NDATAPTS
*VFIL,THETAP(1),RAMP,0,360/NDATAPTS
*ELSE
*GET,DEFINED,PARM,THETAP,DIM,X
*IF,DEFINED,NE,NDATAPTS,THEN
*DIM,THETAP,TABLE,NDATAPTS
*VFIL,THETAP(1),RAMP,0,360/NDATAPTS
*ENDIF
*ENDIF

*GET,DEFINED,PARM,FINP,TYPE
*IF,DEFINED,NE,2,THEN
*DIM,FINP,TABLE,NDATAPTS
*ELSE
*GET,DEFINED,PARM,FINP,DIM,X
*IF,DEFINED,NE,NDATAPTS,THEN
*DIM,FINP,TABLE,NDATAPTS
*ENDIF
*ENDIF

*GET,DEFINED,PARM,FOUTP,TYPE
*IF,DEFINED,NE,2,THEN
*DIM,FOUTP,TABLE,NDATAPTS
*ELSE
*GET,DEFINED,PARM,FOUTP,DIM,X
*IF,DEFINED,NE,NDATAPTS,THEN
*DIM,FOUTP,TABLE,NDATAPTS
*ENDIF
*ENDIF

C*** FILL MODE, ISYM, AND THETA ARRAYS
*VFIL,MODE(1),RAMP,0,1
*VFIL,MODE(NMDS+2),RAMP,1,1
*VFIL,ISYM(1),RAMP,1
*VFIL,ISYM(NMDS+2),RAMP,-1
*VFIL,THETA(1),RAMP,0,360/NDATAPTS

C*** SAMPLE F(THETA) FOR TESTING PURPOSES:
FNCTNIN(1) = 0.4,1.3,3.6,7.5,9.5,10,9.5,7.5,3.6,1.3
FNCTNIN(11) = 0.4
*VFIL,FNCTNIN(12),RAMP,0

C*** CALCULATE FOURIER COEFFICIENTS
*MFOURI,FIT,COEFF(1),MODE(1),ISYM(1),THETA(1),FNCTNIN(1)

C*** EVALUATE TRUNCATED FOURIER SERIES
*MFOURI,EVAL,COEFF(1),MODE(1),ISYM(1),THETA(1),FNCTNOUT(1)

C*** FILL PLOTTING "TABLE" ARRAYS
*DO,I,1,NDATAPTS
 FINP(I) = FNCTNIN(I)
 FOUTP(I) = FNCTNOUT(I)
 THETAP(I) = THETA(I)
*ENDDO

C*** PLOT TABLE ARRAYS TO SEE HOW WELL FOURIER XFRM REPRESENTS FUNCTION
/YRAN,-2,13 ! PROBLEM DEPENDENT FIXED RANGE; REMOVE OR EDIT TO SUIT
/XRAN,0,20
/GRID,1
*VPLO,THETAP(1),FINP(1)
/NOE
*VPLO,THETAP(1),FOUTP(1)
/ERA

/EOF ! TEMPORARY END-OF-FILE: WHAT FOLLOWS IS MODEL-DEPENDENT

C*** APPLY LOAD
*DO,I,1,2*NMDS+1
 *IF,I,EQ,1,THEN          ! APPLY "DC" (AXISYMMETRIC) LOADS HERE
  F,NODE NUMBER,FX,(DC VALUE)/COEFF(I) ! HYPOTHETICAL AXISYMM LOAD
 *ELSE
  FDEL,NODE NUMBER,HEAT ! DELETE "DC" LOAD FOR MODE > 0
 *ENDIF
 MODE,MODE(I),ISYM(I)
 F,NODE NUMBER,FX,COEFF(I) ! APPLY NON-AXISYMM LOAD FOR ALL MODE #'S > 0
 LSWRITE,I
*ENDDO

C*** SOLVE
ALLS
LSSOLVE,1,2*NMDS+1
FINI

C*** POSTPROCESS
/POST1
*AFUN,DEG

ANGLE = 45 ! ANGLE AT WHICH RESULTS ARE DESIRED

C*** DEFINE LOAD CASES, ASSIGN COEFFICIENTS
*DO,I,1,2*NMDS+1
 LCDEF,I,I
 *IF,ISYM(I),EQ,1,THEN
  LCFACT,I,COS(MODE(I)*ANGLE)
 *ELSEIF,ISYM(I),EQ,-1,THEN
  LCFACT,I,SIN(MODE(I)*ANGLE)
 *ENDIF
*ENDDO

LCASE,1 ! READ IN FIRST ("DC") RESULTS SET

C*** COMBINE REMAINING RESULTS ON RESULTS FILE WITH
C*** FIRST RESULTS SET
*DO,I,2,2*NMDS+1
 LCOP,ADD,I
*ENDDO

C*** MAKE PLOTS, ETC
PLNS,S,X ...