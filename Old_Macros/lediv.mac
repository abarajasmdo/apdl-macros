/nopr
! 
! LEDIV.MAC  A MACRO TO DIVIDE A LINE ELEMENT INTO TWO OR MORE LINE
!            ELEMENTS.
!
! Written by: John Crawford       Last updated: 11-08-93
!
! USAGE:    LEDIV, ELEMENT NUMBER, NUMBER OF ELEMENTS TO BE CREATED
!
!
*IF,ARG2,LE,1,:MES1
!
*GET,NMAX,MXND             ! MXND=MAX NODE IN MODEL (UNDOCUMENTED).
*GET,N1,ELEM,ARG1,NODE,1
*GET,N2,ELEM,ARG1,NODE,2
*GET,N3,ELEM,ARG1,NODE,3
!
NJ=NMAX+1
FILL,N1,N2,ARG2-1,NJ,1
!
*GET,R1,ELEM,ARG1,ATTR,REAL
*GET,M1,ELEM,ARG1,ATTR,MAT
*GET,T1,ELEM,ARG1,ATTR,TYPE
*GET,ETNAME,ETYP,T1,ATTR,ENAM
!
!
*IF,ETNAME,NE,18,:BRANCH   ! IS LINE ELEMENT A PIPE18.
NB=NJ
NDIST,N3,N1,1         ! FIND RADIUS.
RADIUS=D
LPMX=ARG2-1           ! MAXIMUM NUMBER OF LOOPS TO BE PERFORMED.
LCNT=0                ! LOOP COUNTER INITIALIZED.
:LP1
LCNT=LCNT+1
NDIST,N3,NB,1
X=RADIUS-D
RATIO=-1+((D-X)/D)
NTN,NB,N3,RATIO,1     ! MOVE NODE.
NB=NB+1
*IF,LCNT,LT,LPMX,:LP1
!
!
:BRANCH
REAL,R1
MAT,M1
TYPE,T1
EDEL,ARG1
E,N1,NJ,N3            ! CREATE THE FIRST ELEMENT.
LPMX=ARG2-2           ! MAXIMUM NUMBER OF LOOPS TO BE PERFORMED.
*IF,LPMX,LE,0,:ELEND  ! CHECK TO SEE IF LOOP SHOULD BE ENTERED.
LCNT=1                ! LOOP COUNTER SET TO 1.
:LP2
NI=NJ
NJ=NI+1
E,NI,NJ,N3
LCNT=LCNT+1
*IF,LCNT,LE,LPMX,:LP2
:ELEND
E,NJ,N2,N3           ! CREATE THE LAST ELEMENT.
:MES1
*MSG,NOTE,ARG2
Created, %I, Elements
/gopr
