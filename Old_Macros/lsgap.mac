!/GOPR
/nopr
!/COM
!/COM  EGEN_GAP3D    ANSYS 5.4   VER 1.0   12/08/98     R.P. ZIEGLER
!/COM
!/COM     GENERATES GAP ELEMENTS TO A FILE. THE I NODES ARE TAKEN FROM THE
!/COM
!/COM     NAMED COMPONENT 'NODEI', AND THE J NODES ARE FOUND BY SEARCHING THE NAMED 
!/COM
!/COM     NAMED COMPONENT 'NODEJ' IN THE ACTIVE COORDINATE SYSTEM USING THE INPUT
!/COM
!/COM     TOLERANCES.
!/COM     
!/COM  REQUIREMENTS :
!/COM
!/COM     CREATE NAMED COMPONENT OF I NODES - NODEI
!/COM
!/COM     CREATE NAMED COMPONENT OF J NODES - NODEJ
!/COM     
!/COM  INPUTS :
!/COM
!/COM     ARG1 - 'Fname'  FILE NAME ( UP TO 8 CHARACTERS ENCLOSED IN SINGLE QUOTES )
!/COM
!/COM     ARG2 - MAT NUMBER  - DEFAULTS TO 1
!/COM
!/COM     ARG3 - TYPE NUMBER - DEFAULTS TO 1
!/COM
!/COM     ARG4 - REAL NUMBER - DEFAULTS TO 1
!/COM
!/COM     ARG5 - X DIRECTION OFFSET - DEFAULTS TO 0
!/COM
!/COM     ARG6 - Y DIRECTION OFFSET - DEFAULTS TO 0
!/COM
!/COM     ARG7 - Z DIRECTION OFFSET - DEFAULTS TO 0
!/COM
!/COM     ARG8 - GEOMETRIC TOLERANCE - DEFAULTS TO 1E-8. COORDINANTE DIFFERENCES
!/COM            SMALLER THAN ARG8 WILL BE SET TO ZERO.
!/COM
!/COM  OUTPUTS :
!/COM
!/COM     GAP ELEMENTS WRITTEN TO FILE ARG1.inp
!/COM
!/COM     GAP ELEMENT DATA ARRAY GAPIJ ( N+1 X 6 ) WERE N IS THE NUMBER OF GAP ELEMENTS
!/COM
!/COM        GAPIJ(I,1) =  GAP ELEMENT NODE I
!/COM
!/COM        GAPIJ(I,2) =  GAP ELEMENT NODE J
!/COM
!/COM        GAPIJ(I,3) =  NX(J) - NX(I) IN ACTIVE COORDINATE SYSTEM
!/COM
!/COM        GAPIJ(I,4) =  NY(J) - NY(I) IN ACTIVE COORDINATE SYSTEM
!/COM
!/COM        GAPIJ(I,5) =  NZ(J) - NZ(I) IN ACTIVE COORDINATE SYSTEM
!/COM
!/COM        GAPIJ(I,6) =  DISTANCE BETWEEN NODES I AND J
!/COM
/NOPR
!/COM
!     INTERNAL SCALAR VARIABLES :
!
!        AR99 -  NDMIN - MINIMUM NODE NUMBER OF COMPONENT NODEI
!
!        AR98 -  NDNUM - NUMBER OF NODES IN COMPONENT NODEI
!
!        AR97 -  NDNUM - NUMBER OF NODES IN COMPONENT NODEJ
!
!
!     INTERNAL ARRAY VARIABLES :
!
!        TMPDATA1 -  TOLERANCE VALUES FOR I NODE IN COMPONENT NODEI
!
!          TMPDATA1(I,1) =  X SEARCH TOLERANCE
!
!          TMPDATA1(I,2) =  Y SEARCH TOLERANCE
!
!          TMPDATA1(I,3) =  Z SEARCH TOLERANCE
!
!
!*IF,ARG1,EQ,0,:END
*get,TSTART,ACTIVE,,TIME,CPU
!
*IF,ARG2,LE,0,THEN
ARG2=1
*ENDIF
!
*IF,ARG3,LE,0,THEN
ARG3=1
*ENDIF
!
*IF,ARG4,LE,0,THEN
ARG4=1
*ENDIF
*IF,ARG8,LE,0,THEN
ARG8=1.0E-6
*ENDIF
!
!
CMSEL,S,NODEJ
*GET,AR97,NODE,,COUNT
CMSEL,S,NODEI
*GET,AR98,NODE,,COUNT
*IF,AR98,NE,AR97,THEN
/GOPR
/COM **************************************************************
/COM
/COM
/COM WARNING  - NUMBER OF NODES IN COMPONENTS  NODEI & NODEJ
/COM            ARE NOT EQUAL
/COM
/COM **************************************************************
/NOPR
*msg,warn,AR97,AR98
There are %I J nodes and %i I nodes
*GO,:END
*ENDIF
!
!
*CFOPEN,%ARG1%,inp
*CFWRITE,MAT,ARG2
*CFWRITE,TYPE,ARG3
*CFWRITE,REAL,ARG4
!
*GET,NDMIN,NODE,,NUM,MIN
*GET,NUMND,NODE,,COUNT
!
*DEL,GAPIJ
*DEL,GAPERROR
*DIM,GAPIJ,ARRAY,(NUMND+1),6,0
*DIM,GAPERROR,ARRAY,NUMND,4
*DIM,TMPDATA1,ARRAY,NUMND,5,0
!
!
GAPIJ(1,1)=NDMIN
*DO,I,1,NUMND,1
GAPIJ(I,3)=NX(GAPIJ(I,1))
GAPIJ(I,4)=NY(GAPIJ(I,1))
GAPIJ(I,5)=NZ(GAPIJ(I,1))
*GET,GAPIJ(I+1,1),NODE,GAPIJ(I,1),NXTH
NSEL,U,NODE,,GAPIJ(I,1)
NDCLOSE=NODE(GAPIJ(I,3),GAPIJ(I,4),GAPIJ(I,5))
TMPDATA1(I,1)=ABS(NX(NDCLOSE)-GAPIJ(I,3))/2
TMPDATA1(I,2)=ABS(NY(NDCLOSE)-GAPIJ(I,4))/2
TMPDATA1(I,3)=ABS(NZ(NDCLOSE)-GAPIJ(I,5))/2
NSEL,A,NODE,,GAPIJ(I,1)
*ENDDO
!
!
NUMERROR=0
*DO,I,1,NUMND,1
CMSEL,S,NODEJ
NSEL,R,LOC,X,(GAPIJ(I,3)+ARG5)-TMPDATA1(I,1),(GAPIJ(I,3)+ARG5)+TMPDATA1(I,1)
NSEL,R,LOC,Y,(GAPIJ(I,4)+ARG6)-TMPDATA1(I,2),(GAPIJ(I,4)+ARG6)+TMPDATA1(I,2)
NSEL,R,LOC,Z,(GAPIJ(I,5)+ARG7)-TMPDATA1(I,3),(GAPIJ(I,5)+ARG7)+TMPDATA1(I,3)
*GET,NUMSEL,NODE,,COUNT
!
*IF,NUMSEL,EQ,1,THEN
!
*GET,GAPIJ(I,2),NODE,,NUM,MIN
!
*ELSEIF,NUMSEL,GT,1,THEN
!
GAPIJ(I,2)=NODE((GAPIJ(I,3)+ARG5),(GAPIJ(I,4)+ARG6),(GAPIJ(I,5)+ARG7))
NUMERROR=NUMERROR+1
GAPERROR(NUMERROR,1)=I
GAPERROR(NUMERROR,2)=GAPIJ(I,1)
GAPERROR(NUMERROR,3)=GAPIJ(I,2)
GAPERROR(NUMERROR,4)=NUMSEL
!
*ELSE
!
CMSEL,S,NODEJ
GAPIJ(I,2)=NODE((GAPIJ(I,3)+ARG5),(GAPIJ(I,4)+ARG6),(GAPIJ(I,5)+ARG7))
NUMERROR=NUMERROR+1
GAPERROR(NUMERROR,1)=I
GAPERROR(NUMERROR,2)=GAPIJ(I,1)
GAPERROR(NUMERROR,3)=GAPIJ(I,2)
GAPERROR(NUMERROR,4)=0
!
*ENDIF
!
GAPIJ(I,3)=NX(GAPIJ(I,2))-GAPIJ(I,3)
GAPIJ(I,4)=NY(GAPIJ(I,2))-GAPIJ(I,4)
GAPIJ(I,5)=NZ(GAPIJ(I,2))-GAPIJ(I,5)
GAPIJ(I,6)=DISTND(GAPIJ(I,1),GAPIJ(I,2))
!
!*CFWRITE,E,GAPIJ(I,1),GAPIJ(I,2),'!', I

ar90=GAPIJ(I,1)
ar91=GAPIJ(I,2)
ar40=GAPIJ(I,6)
!*VWRITE,GAPIJ(I,1),GAPIJ(I,2),I
*VWRITE,ar90,ar91,ar40
('E,',I6,',',I6,5X,'!',1X,f10.8)
!
*IF,ABS(GAPIJ(I,3)),LT,ARG8,THEN
GAPIJ(I,3)=0
*ENDIF
*IF,ABS(GAPIJ(I,4)),LT,ARG8,THEN
GAPIJ(I,4)=0
*ENDIF
*IF,ABS(GAPIJ(I,5)),LT,ARG8,THEN
GAPIJ(I,5)=0
*ENDIF
*IF,ABS(GAPIJ(I,6)),LT,ARG8,THEN
GAPIJ(I,6)=0
*ENDIF
GAPIJ(NUMND+1,3)=GAPIJ(NUMND+1,3)+GAPIJ(I,3)
GAPIJ(NUMND+1,4)=GAPIJ(NUMND+1,4)+GAPIJ(I,4)
GAPIJ(NUMND+1,5)=GAPIJ(NUMND+1,5)+GAPIJ(I,5)
GAPIJ(NUMND+1,6)=GAPIJ(NUMND+1,6)+GAPIJ(I,6)
*ENDDO
GAPIJ(NUMND+1,3)=GAPIJ(NUMND+1,3)/NUMND
GAPIJ(NUMND+1,4)=GAPIJ(NUMND+1,4)/NUMND
GAPIJ(NUMND+1,5)=GAPIJ(NUMND+1,5)/NUMND
GAPIJ(NUMND+1,6)=GAPIJ(NUMND+1,6)/NUMND
!
CMSEL,S,NODEI
CMSEL,A,NODEJ
*CFCLOSE
*DEL,TMPDATA1
*IF,NUMERROR,EQ,0,THEN
*DEL,GAPERROR
*ENDIF
*get,TSTOP,ACTIVE,,TIME,CPU
TIMEDEL=TSTOP-TSTART
!
:END
!
/GOPR
/EOF
