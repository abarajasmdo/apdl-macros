!
!                                                                    MACRO
!
!	REMOVES CURVATURE OF SOLID92 ELEMENTS THAT ARE SELECTED
!
FINISH
!
!       MUST BE IN PREP7 TO FIX NODE LOCATIONS
!
/PREP7
!
!
!       DEACTIVATE SOLID MODEL CHECKING
MODMSH,NOCHECK
!
!     MAKE SELECTED SET A COMPONENT
!
CM,_BADEL,ELEM
CM,_NODES,NODES
NSLE,S
!
!     GET THE NUMBER OF SELECTED ELEMENTS
!
*GET,_MAXEL,ELEM,,COUNT
!
!     GET THE ACTIVE COORDINATE SYSTEM NUMBER
!
*GET,_ACSYS,ACTIVE,,CSYS
!
!     SET THE ACTIVE COORDINATE SYSTEM TO GLOBAL CARTESIAN
!
CSYS,0
!
!     LOOP THRU ALL POOR ELEMENTS AND MOVE ALL MIDSIDES TO
!     THE EXACT MIDSIDE OF THE CHORD
!
*DO,I,1,_MAXEL
!
!	GET MIN ELEMENT NUMBER OF THE SELECTED ELEMENTS
!
*GET,_MINEL,ELEM,,NUM,MIN
*GET,_NI,ELEM,_MINEL,NODE,1
*GET,_NJ,ELEM,_MINEL,NODE,2
*GET,_NK,ELEM,_MINEL,NODE,3
*GET,_NL,ELEM,_MINEL,NODE,4
*GET,_NM,ELEM,_MINEL,NODE,5
*GET,_NN,ELEM,_MINEL,NODE,6
*GET,_NO,ELEM,_MINEL,NODE,7
*GET,_NP,ELEM,_MINEL,NODE,8
*GET,_NQ,ELEM,_MINEL,NODE,9
*GET,_NR,ELEM,_MINEL,NODE,10
!
!   LOOP THRU ALL THE SIDES, CHECKING FOR 0 NODE NUMBERS AS WELL
!
!  SIDE I-J  (NODE M)
!
*IF,_NM,NE,0,THEN
_XV=(NX(_NI)+NX(_NJ))/2.
_YV=(NY(_NI)+NY(_NJ))/2.
_ZV=(NZ(_NI)+NZ(_NJ))/2.
!
!  UPDATE NODE LOCATION
!
NMOD,_NM,_XV,_YV,_ZV
!
*ENDIF
!
!  SIDE J-K (NODE N)
!
*IF,_NN,NE,0,THEN
_XV=(NX(_NJ)+NX(_NK))/2.
_YV=(NY(_NJ)+NY(_NK))/2.
_ZV=(NZ(_NJ)+NZ(_NK))/2.
!
!  UPDATE NODE LOCATION
!
NMOD,_NN,_XV,_YV,_ZV
!
*ENDIF
!
!  SIDE I-K  (NODE O)
!
*IF,_NO,NE,0,THEN
_XV=(NX(_NI)+NX(_NK))/2.
_YV=(NY(_NI)+NY(_NK))/2.
_ZV=(NZ(_NI)+NZ(_NK))/2.
!
!  UPDATE NODE LOCATION
!
NMOD,_NO,_XV,_YV,_ZV
!
*ENDIF
!
!  SIDE I-L  (NODE P)
!
*IF,_NP,NE,0,THEN
_XV=(NX(_NI)+NX(_NL))/2.
_YV=(NY(_NI)+NY(_NL))/2.
_ZV=(NZ(_NI)+NZ(_NL))/2.
!
!  UPDATE NODE LOCATION
!
NMOD,_NP,_XV,_YV,_ZV
!
*ENDIF
!
!  SIDE J-L   (NODE Q)
!
*IF,_NQ,NE,0,THEN
_XV=(NX(_NJ)+NX(_NL))/2.
_YV=(NY(_NJ)+NY(_NL))/2.
_ZV=(NZ(_NJ)+NZ(_NL))/2.
!
!  UPDATE NODE LOCATION
!
NMOD,_NQ,_XV,_YV,_ZV
!
*ENDIF
!
!  SIDE K-L  (NODE R)
!
*IF,_NR,NE,0,THEN
_XV=(NX(_NK)+NX(_NL))/2.
_YV=(NY(_NK)+NY(_NL))/2.
_ZV=(NZ(_NK)+NZ(_NL))/2.
!
!  UPDATE NODE LOCATION
!
NMOD,_NR,_XV,_YV,_ZV
!
*ENDIF
!
!      REMOVE ELEMENT FROM SELECTED SET
!
ESEL,U,ELEMENT,,_MINEL
!
*ENDDO
MODMSH,CHECK
CSYS,_ACSYS
CMSEL,S,_BADEL
CMSEL,S,_NODES
