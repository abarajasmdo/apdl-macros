!  
! macro mk52.mac  This macro builds STIF 52 gaps between flange joints that 
!   have an interference.  This is done by creating a set of dummy nodes based
!	on the aft surface to get the stif 52 direction correct.  The aft
!	node is coupled to the psuedo aft node and a stif 52 is built
!	between the fwd node and the pseudo aft node for the correct dir.
! 
!  usage :  rabbet
!
! prep:  have component of inner nodes outer nodes, have stif 21 type defined,
!	stif52 type defined, cylindrical system selected, real constant for 
!	stif52 and 21 msut be defined
!
!	POSSIBLE ENHANCEMENTS.  NMOD NGEN'D NODES AND USE ETINF TO GENERATE
!	FINAL GAPS BETWEEN PSUEDO NODES AND FWD NODES VS. ONE AT A TIME.
!
!  prep
PARSAV
csys,60
eall
einv  ! unselect all elems
CMSEL,,AFT	! SMALL z COORD  (csys 11)
CMSEL,A,FWD	! LARGER z COORD  (csys,11)
NROTAT,ALL	!  MUST ROTAT FOR CPINTF TO WORK!
*GET,LNUM,NODE,,NUM,MIN
*GET,NMAX,MXND
INC=NMAX-LNUM+10
*ASK,DISP,TOLER FOR GENERATING INITIAL GAPS,.0001
TYPE,52
EINTF,DISP	! make temp 52's to use as definition 
CM,_ETEMP_,ELEM
*GET,ENUM,ELEM,,COUNT
E=ELNEXT(0)
NGEN,2,INC,AFT,,,,,-.01  ! CREATE PSUEDO AFT NODE -.01 aft, INCREMENT=INC
CM,_NTEMP_,NODE
NROTAT,ALL   !  ROTATE IN CYLIN SYSTEM
!
! LOOP CREATES PSUEDO ELEMENT, COUPLES, AND STIF 52'S
!
*DO,IT,1,ENUM
ESEL,R,,,E
NELEM
CMSEL,R,AFT
NAFT=NDNEXT(0)
NELEM
CMSEL,U,AFT
NFWD=NDNEXT(0)
!
!  TWEAK FWD NODES TO ALIGN EXACTLY WITH AFT
!
NMOD,NFWD,NX(NAFT),NY(NAFT),,
TYPE,21
REAL,21
CMSEL,,_NTEMP_
E,NAFT+INC	!  MAKE DUMMY STIF 21'S AT PSUEDO NODES (REQUIRED)
CP,NEXT,UX,NAFT,NAFT+INC
,NEXT,UY,NAFT,NAFT+INC
,NEXT,UZ,NAFT,NAFT+INC
TYPE,52
REAL,60
E,NFWD,NAFT+INC
CMSEL,,_ETEMP_
E=ELNEXT(E)
*ENDDO
CMSEL,,_NTEMP_
NROTAT,ALL
!
! CLEAN UP
!
CMSEL,,_ETEMP_
CMDELE,_ETEMP_
CMDELE,_NTEMP_
EDELE,ALL
PARRES,NEW
