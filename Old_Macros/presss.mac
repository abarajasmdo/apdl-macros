From - Mon Apr 19 11:19:59 1999
Received: from gemb1.ae.ge.com ([129.202.7.246]) by evbhdmsxaege.ae.ge.com with SMTP (Microsoft Exchange Internet Mail Service Version 5.5.2448.0)
	id 26M997LN; Tue, 13 Apr 1999 14:35:08 -0400
Received: from [3.47.28.17] by gemb1.ae.ge.com; (5.65/1.1.8.2/04Nov94-0158PM)
	id AA19963; Tue, 13 Apr 1999 14:29:39 -0400
Received: from ns.ge.com (ns-i.ge.com [10.35.39.24])
	by thomas3.ge.com (8.9.3/8.9.3) with ESMTP id OAA15298
	for <dan.bohlen@ae.ge.com>; Tue, 13 Apr 1999 14:31:28 -0400 (EDT)
Received: from onelist.com (pop.onelist.com [209.207.164.229])
	by ns.ge.com (8.9.3/8.9.3) with SMTP id OAA15874
	for <dan.bohlen@ae.ge.com>; Tue, 13 Apr 1999 14:27:02 -0400 (EDT)
Received: (qmail 6592 invoked by alias); 13 Apr 1999 14:43:00 -0000
Received: (qmail 6584 invoked from network); 13 Apr 1999 14:42:58 -0000
Received: from unknown (HELO plmler1.mail.eds.com) (199.228.142.73) by pop.onelist.com with SMTP; 13 Apr 1999 14:42:58 -0000
Received: from nnsp.eds.com (nnsp2.eds.com [199.228.143.130]) by plmler1.mail.eds.com (8.9.1a/8.9.1) with ESMTP id KAA13176 for <xansys@onelist.com>; Tue, 13 Apr 1999 10:26:52 -0400 (EDT)
Received: from jumtcia0.mex.gmeds.com (jumtcia0.mex.gmeds.com [205.191.40.100]) by nnsp.eds.com (8.9.1a/8.9.1) with ESMTP id KAA11423 for <xansys@onelist.com>; Tue, 13 Apr 1999 10:26:21 -0400 (EDT)
Received: from mex.gmeds.com (cadhe039 [205.191.41.39]) by jumtcia0.mex.gmeds.com with ESMTP (8.7.1/8.7.3) id IAA03387 for <xansys@onelist.com>; Tue, 13 Apr 1999 08:22:11 -0600 (MDT)
Sender: tzdghr@mex.gmeds.com
Message-Id: <37135444.A27FFB02@mex.gmeds.com>
Date: Tue, 13 Apr 1999 08:27:16 -0600
From: Susan Maxwell <susan.maxwell@mex.gmeds.com>
Organization: Delphi Automotive Systems
X-Mailer: Mozilla 4.03 [en] (X11; I; HP-UX B.10.20 9000/782)
To: xansys@onelist.com
References: <199904131010.MAA07772@mailserver2.hrz.tu-darmstadt.de>
Mailing-List: list xansys@onelist.com; contact xansys-owner@onelist.com
Delivered-To: mailing list xansys@onelist.com
Precedence: bulk
List-Unsubscribe: <mailto:xansys-unsubscribe@ONElist.com>
Reply-To: xansys@onelist.com
Mime-Version: 1.0
Content-Type: multipart/mixed; boundary="------------56C7B7B5D69A2E0C21CB45E2"
Subject: [xansys] Re: non-unifrom pressure
X-Mozilla-Status: 8001
X-Mozilla-Status2: 00000000
X-UIDL: AAw2MyiAAAwNGbACgBg3YGR0aMGXHKwA

This is a multi-part message in MIME format.
--------------56C7B7B5D69A2E0C21CB45E2
Content-Type: text/plain; charset=us-ascii
Content-Transfer-Encoding: 7bit

Mark Weinreuter wrote:

> From: "Mark Weinreuter" <mawei@HRZ3.HRZ.TU-Darmstadt.De>
>
> Does anybody know how to apply a non-uniform pressure to a hole in
> Ansys5.5?
>

Mark,

Here is a macro that was published on this list around March 31, 1999.  I
think it may do what you would like (as well as a number of other useful
loading schemes).  I thought it was very well written and documented.
Unfortunately, I don't recall who wrote it, so I can't tell you who to
thank.  I bet you could search the archives and find out who wrote it.

Susan



--------------56C7B7B5D69A2E0C21CB45E2
Content-Type: text/plain; charset=us-ascii; name="distload.mac"
Content-Transfer-Encoding: 7bit
Content-Disposition: inline; filename="distload.mac"

!	MACRO FILE DISTLOAD.MAC
!
!	PROMPTS THE USER THROUGH APPLYING DISTRIBUTED LOADS
!	THAT ARE USER-DEFINED FUNCTIONS OF POSITION.  THESE FUNCTIONS
!	ARE DEFINED BY THE USER, USING THE APDL LANGUAGE, AND ARE PLACED
!	IN FILES.  THE USER WILL BE PROMPTED FOR THE TYPE OF LOADING AND THE 
!           FILENAME
!
!	ALL LOADS ASSUME THAT X,Y,Z ARE TO BE INTERPRETED IN THE COORDINATE 
!           SYSTEM SPECIFIED BY THE USER. X,Y, AND Z ARE THE COORDINATES OF THE
!           SELECTED NODES FOR NODAL BASED LOADS(SF,D,F,BF) AND THE 
!           COORDINATES OF THE ELEMENT CENTROID FOR ELEMENT BASED 
!           LOADS(SFE,BFE,SFBEAM)
!
!	USER FILE WILL CONTAIN INFORMATION IN ONE OF THE FOLLOWING FORMATS:
!
!		. SURFACE LOADS USING NODES(SF type)
!			FILE CONTAINS:
!	  		         QLAB(1)='lab'      WHERE lab is PRES,CONV, or FLUX
!			         QVAL1=f1(x,y,z)  WHERE f1 DEFINES THE DISTRIBUTION
!			         QVAL2=f2(x,y,z)  WHERE f2 DEFINES THE OTHER PORTION
!				                        OF THE DISTRIBTION, BULK TEMPERATURE
!						WHEN lab IS CONV, QVAL2 CAN BE 
!                                                                       OMITTED WHEN lab IS FLUX OR PRES
!
!			FOR EXAMPLE
!				QLAB(1)='PRES'
!				PI=ACOS(-1)
!				QVAL1=1000*(COS(2*PI*X/20)+400)
!
!
!		. ELEMENT LOADS(SFE,SFBEAM,BFE type)
!		    FILE CONTAINS:
!		   	SFE,XNM,LKEY,LAB,KVAL,F1(X,Y,Z),F2(X,Y,Z),F3(X,Y,Z),F4(X,Y,Z)
!			BFE,XNM,LAB,STLOC,F1(X,Y,Z),F2(X,Y,Z),F3(X,Y,Z),F4(X,Y,Z)
!			SFBEAM,XNM,LKEY,LAB,F1(X,Y,Z),F2(X,Y,Z),,,F5(X,Y,Z),F6(X,Y,Z)
!
!			FOR EXAMPLE
!				SFE,XNM,1,PRES,,100*(2X+3Y-6Z)
!                                                                                                 
!					XMN IS A NODE NUMBER THAT THE MACRO WILL
!					SUPPLY, DO NOT PUT IN A NODE NUMBER OR USE
!					ANOTHER VARIABLE NAME
!
!		. NODAL LOADS(D,F,BF TYPE)
!			FILE CONTAINS:
!				D,XNM,LAB,F1(X,Y,Z),F2(X,Y,Z),,,LAB2...LAB6
!				F,XNM,LAB,F1(X,Y,Z),F2(X,Y,Z)
!				BF,XNM,LAB,F1(X,Y,Z),F2(X,Y,Z),F3(X,Y,Z),F4(X,Y,Z)
!
!			FOR EXAMPLE
!				D,XNM,UX,.01*(2X+3Y-6Z)
!
/NOPR
*SET,IFILE
*SET,QLAB
*SET,QPAR1
*SET,QPAR2
*DIM,IFILE,CHAR,1
*DIM,QLAB,CHAR,1
CM,LFNOD,NODE
CM,LFELE,ELEM
NSEL,ALL
*GET,NMXMDL,NODE,,NUM,MAX
CMSEL,S,LFNOD
*DIM,QPAR1,ARRAY,NMXMDL
*DIM,QPAR2,ARRAY,NMXMDL
*GET,ACCSY,ACTIVE,,CSYS
*GET,ACDSY,ACTIVE,,DSYS
*MSG,INFO

*MSG,INFO
*******************************************************************
*MSG,INFO
Macro to apply bc's using a user-specified distribution %/
*MSG,INFO
Distribution is defined as a function of X, Y, and Z coordinate
*MSG,INFO
 values in a previously defined coordinate system. %/
*MSG,INFO
Boundary conditions are applied only to selected nodes and elements. %/
*MSG,INFO

*MSG,INFO
*******************************************************************
*MSG,INFO
Enter type of loading %/
*MSG,INFO
Enter 0 for surface loads on selected elements using
*MSG,INFO
selected nodes to define the surface (SF) %/
*MSG,INFO
Enter 1 for element loads (SFE, SFBEAM, BFE) %/
*MSG,INFO
Enter 2 for nodal loads (D, F, BF) %/
*ASK,TKEY,ENTER:0->SF; 1->SFE; or 2->D/BF/F,0
*IF,TKEY,LT,0,THEN
*MSG,WARN,
TYPE IS NOT EQUAL TO 1 OR 0, SURFACE LOADS USING NODES WILL BE APPLIED
TKEY=0
ELSEIF,TKEY,GT,2,THEN
*MSG,WARN,
TYPE IS NOT EQUAL TO 1 OR 0, SURFACE LOADS USING NODES WILL BE APPLIED
TKEY=0
*ENDIF
*ASK,FCS,CS IN WHICH LOADING IS DEFINED,0
*GET,CDEF,CDSY,FCS,ATTR,KCS
*IF,CDEF,EQ,-1,THEN
*MSG,WARN,FCS
COORDINATE SYSTEM %I IS NOT DEFINED. GLOBAL COORDINATES WILL BE USED
FCS=0
*ENDIF
CSYS,FCS
DSYS,FCS
*MSG,INFO
Specify file name which defines load specification. 
*IF,TKEY,EQ,1,THEN
*MSG,INFO
Data in this file must be of the form: %/
*MSG,INFO
SFE,XNM,LKEY,LAB,KVAL,F1(X,Y,Z),F2(X,Y,Z),F3(X,Y,Z),F4(X,Y,Z)
*MSG,INFO
OR %/
*MSG,INFO
BFE,XNM,LAB,STLOC,F1(X,Y,Z),F2(X,Y,Z),F3(X,Y,Z),F4(X,Y,Z)
*MSG,INFO
OR %/
*MSG,INFO
SFBEAM,XNM,LKEY,LAB,F1(X,Y,Z),F2(X,Y,Z),,,F5(X,Y,Z),F6(X,Y,Z)
*MSG,INFO
where F(X,Y,Z) is the user-defined load distribution (using APDL).
*MSG,INFO
(X,Y,Z) are the coordinates of the element centroid
*MSG,INFO
For example: %/
*MSG,INFO
 SFE,XNM,1,PRES,,100*(2X+3Y-6Z) %/
*ELSEIF,TKEY,EQ,2,THEN
*MSG,INFO
Must be of the form: %/
*MSG,INFO
D,XNM,LAB,F1(X,Y,Z),F2(X,Y,Z),,,LAB2...LAB6
*MSG,INFO
OR %/
*MSG,INFO
F,XNM,LAB,F1(X,Y,Z),F2(X,Y,Z)
*MSG,INFO
OR %/
*MSG,INFO
BF,XNM,LAB,F1(X,Y,Z),F2(X,Y,Z),F3(X,Y,Z),F4(X,Y,Z)
*MSG,INFO
where F(X,Y,Z) is the user-defined load distribution (using APDL).
*MSG,INFO
(X,Y,Z) are the coordinates of the nodes
*MSG,INFO
For example: %/
*MSG,INFO
D,XNM,UX,.01*(2X+3Y-6Z) %/
*ELSEIF,TKEY,EQ,0,THEN
*MSG,INFO
Must be of the form: %/
*MSG,INFO
QLAB(1)='lab'
*MSG,INFO
QVAL1=F1(X,Y,Z)
*MSG,INFO
QVAL2=F2(X,Y,Z) %/
*MSG,INFO
where:
*MSG,INFO
'lab' is any valid surface load label
*MSG,INFO
F1(X,Y,Z) is user defined load distribution (using APDL)
*MSG,INFO
F2(X,Y,Z) is bulk temp. distribution if 'lab' = CONV %/
*MSG,INFO
(X,Y,Z) are the coordinates of the nodes
*MSG,INFO
Surface loads are applied using the SF command on the selected
*MSG,INFO
set of nodes and elements. For example: %/
*MSG,INFO
QLAB(1)='PRES'
*MSG,INFO
PI=ACOS(-1)
*MSG,INFO
QVAL1=1000*(COS(2*PI*X/20)+400)
*ENDIF
*ASK,IFILE(1),file name in quotes (< 9 char.),pfile
*IF,TKEY,EQ,1,THEN
*GET,EMX,ELEM,,COUNT
*GET,EMIN,ELEM,,NUM,MIN
ENUM=EMIN-1
*DO,I,1,EMX
ENUM=ELNEXT(ENUM)
*GET,X,ELEM,ENUM,CENT,X
*GET,Y,ELEM,ENUM,CENT,Y
*GET,Z,ELEM,ENUM,CENT,Z
XNM=ENUM
/INPUT,%IFILE(1)%
*ENDDO
*ELSEIF,TKEY,EQ,2,THEN
*GET,NMX,NODE,,COUNT
*GET,NMIN,NODE,,NUM,MIN
NNUM=NMIN-1
*DO,I,1,NMX
NNUM=NDNEXT(NNUM)
X=NX(NNUM)
Y=NY(NNUM)
Z=NZ(NNUM)
XNM=NNUM
/INPUT,%IFILE(1)%
*ENDDO
*ELSEIF,TKEY,EQ,0,THEN
QVAL2=0
*GET,NMX,NODE,,COUNT
*GET,NMIN,NODE,,NUM,MIN
NNUM=NMIN-1
*DO,I,1,NMX
NNUM=NDNEXT(NNUM)
X=NX(NNUM)
Y=NY(NNUM)
Z=NZ(NNUM)
/INPUT,%IFILE(1)%
QPAR1(NNUM)=QVAL1
QPAR2(NNUM)=QVAL2
*ENDDO
SFFUN,%QLAB(1)%,QPAR1(1),QPAR2(1)
SF,ALL,%QLAB(1)%
SFFUN
*ENDIF
CSYS,ACCSY
DSYS,ACDSY
CMSEL,S,LFNOD
CMSEL,S,LFELE
/GOPR

--------------56C7B7B5D69A2E0C21CB45E2--
