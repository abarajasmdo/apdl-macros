/nopr
!
!  seis3.mac      
!
!   usage:     seis3,mode   
!
!    where:  arg1=the mode that stresses and deflections are calculated for.
!
!  a macro to calculate stresses and forces and moments
!  due to 3-D excitation
!
!  it also calculates the worst case von mises stresses for pipe16 elements
!
!  this has been preceeded by doing a modal analysis and getting
!  the mode coefficients mcx, mcy and mcz using the macros
!  mcx.mac, mcy.mac and mcz.mac.  
!
n=arg1
lczero
!   define load cases with scaling factors
lcdef,1,1,n
lcfact,1,mcx(n)   !  lcase1 is the x direction results
lcdef,2,1,n
lcfact,2,mcy(n)   !  lcase2 is the y direction results
lcdef,3,1,n
lcfact,3,mcz(n)   !  lcase3 is the z direction results
!
!   select only the pipe elements
esel,s,ename,,16,18   
!
!   start setting up the etable
etab,vmmax,nmisc,89    !  max von mises stress in each element
,fxi,smisc,1
,fyi,smisc,2
,fzi,smisc,3           !  forces as moments at i and j ends of elements
,mxi,smisc,4
,myi,smisc,5
,mzi,smisc,6
,fxj,smisc,7
,fyj,smisc,8
,fzj,smisc,9
,mxj,smisc,10
,myj,smisc,11
,mzj,smisc,12!
!
!  select all the elements
!
esel,all
!
lcase,1   
etab,pout,smisc,22
etab,refl
sadd,vm1,pout,vmmax
lcoper,squa  !  square x direction results
lcwrite,4      !  store them in load case 4
!
lcase,2
etab,refl
sadd,vm2,pout,vmmax
lcoper,squa  !  square y direction results
lcwrite,5      !  store them in load case 5
!
lcase,3
etab,refl
sadd,vm3,pout,vmmax
lcoper,squa  !  square z direction results
lcwrite,6      !  store them in load case 6
!
lcase,4        !  sum the squares and take the square root
lcoper,add,5   
lcoper,add,6
lcoper,sqrt
lcwrite,7     !  save it as lcase 7
!
!  operations to calculate the worst case von mises stresses
!  
smult,vm4,vm1,vm1  !  von mises stress 1 squared
smult,vm5,vm2,vm2  !  von mises stress 2 squared
smult,vm6,vm3,vm3  !  von mises stress 3 squared
sadd,vmsq,vm4,vm5
sadd,vmsq,vmsq,vm6  !  sum of von mises stresses squared
sexp,vm,vmsq,,.5     !  worst case von mises stress is variable vm
!
!
!
!  
/gopr




